LIBNAME DEEPD "\\WPUSHH01\DINFOPLN\MARKETING MIX PI\INVOPT\P2 2014 AB\PROMO\DIABETES DEEP DIVE\SAS WORK";

******************************************************************************************************************************************;
* IMPORT PROMOTION SPEND, DIRECT SELLING AND RX VOLUME DATA FROM EXCEL "LONG GRAPHS" SPREADSHEET. TRANSPOSE, RENAME AND WEIGHT 2009/10.   ;
******************************************************************************************************************************************;

* BRING EXCEL DATA INTO SAS FROM FOUR SPREADSHEETS;
%MACRO IMPEM (FNAME,RNAME);
	PROC IMPORT OUT= WORK.&FNAME 
	DATAFILE= "\\WPUSHH01\DINFOPLN\MARKETING MIX PI\INVOPT\P2 2014 AB\PROMO\DIABETES DEEP DIVE\DIABETES PROMO DETAIL 2009-2013 LONG GRAPHS.XLS"
	DBMS=EXCEL REPLACE; RANGE="&RNAME"; GETNAMES=YES; MIXED=NO; SCANTEXT=YES; USEDATE=YES; SCANTIME=YES; RUN;
%MEND;
%IMPEM (CAT,CATEGORY$); 					*PROMOTION SPEND BY CATEGORY;
%IMPEM (SUBCAT,SUB-CATEGORY$); 				*PROMOTION SPEND BY SUB-CATEGORY;
%IMPEM (NPA,RX VOLUME (NPA)$); 				*RX VOLUME FROM BADS DRIVE;
%IMPEM (GRAIL,RX VOLUME & PROMO (GRAIL)$); 	*RX VOLUME AND PROMOTION FROM GRAIL;

PROC CONTENTS DATA=CAT VARNUM; PROC CONTENTS DATA=SUBCAT VARNUM; PROC CONTENTS DATA=NPA VARNUM; PROC CONTENTS DATA=GRAIL VARNUM; RUN;

/*
* TRANSPOSE...VAR NAMES ARE EXCEL DATES WITH FIRST CHARACTER REPLACED BY UNDERSCORE (EXCEL TO SAS DATE TRANSFORMATION IN NEXT DATA STEP);
* VAR USED TO FORCE TRANSPOSITION OF ALL MONTHS IN EACH FILE DESPITE MISSING VALUES...MISSING VALUES RESULT IN VARS AS CHARACTER FORMAT;
%MACRO TRANEM (FNAME, VTYPE);
	PROC TRANSPOSE DATA=&FNAME OUT=&FNAME._T; ID &VTYPE; 
	 VAR _9814 _9845 _9873 _9904 _9934 _9965 _9995 _0026 _0057 _0087 _0118 _0148 
		 _0179 _0210 _0238 _0269 _0299 _0330 _0360 _0391 _0422 _0452 _0483 _0513 
		 _0544 _0575 _0603 _0634 _0664 _0695 _0725 _0756 _0787 _0817 _0848 _0878 
		 _0909 _0940 _0969 _1000 _1030 _1061 _1091 _1122 _1153 _1183 _1214 _1244 
		 _1275 _1306 _1334 _1365 _1395 _1426 _1456 _1487 _1518 _1548 _1579 _1609;
%MEND;
%TRANEM(CAT,CATEGORY);
%TRANEM(SUBCAT,SUB_CATEGORY_LABEL);
%TRANEM(NPA,REC_TYPE);
%TRANEM(GRAIL,REC_TYPE); RUN;
*/
* TRANSPOSE...VAR NAMES ARE "MMM_YY";
* VAR USED TO FORCE TRANSPOSITION OF ALL MONTHS IN EACH FILE DESPITE MISSING VALUES...MISSING VALUES RESULT IN VARS AS CHARACTER FORMAT;
%MACRO TRANEM2 (FNAME, VTYPE);
	PROC TRANSPOSE DATA=&FNAME OUT=&FNAME._T; ID &VTYPE; 
	 VAR Jan_09 Feb_09 Mar_09 Apr_09 May_09 Jun_09 Jul_09 Aug_09 Sep_09 Oct_09 Nov_09 Dec_09 
	     Jan_10 Feb_10 Mar_10 Apr_10 May_10 Jun_10 Jul_10 Aug_10 Sep_10 Oct_10 Nov_10 Dec_10 
	     Jan_11 Feb_11 Mar_11 Apr_11 May_11 Jun_11 Jul_11 Aug_11 Sep_11 Oct_11 Nov_11 Dec_11 
	     Jan_12 Feb_12 Mar_12 Apr_12 May_12 Jun_12 Jul_12 Aug_12 Sep_12 Oct_12 Nov_12 Dec_12 
	     Jan_13 Feb_13 Mar_13 Apr_13 May_13 Jun_13 Jul_13 Aug_13 Sep_13 Oct_13 Nov_13 Dec_13; 
%MEND TRANEM2;
%TRANEM2(CAT,CATEGORY);
%TRANEM2(SUBCAT,SUB_CATEGORY_LABEL);
%TRANEM2(NPA,REC_TYPE);
%TRANEM2(GRAIL,REC_TYPE); RUN;


PROC CONTENTS DATA=CAT_T VARNUM; PROC CONTENTS DATA=SUBCAT_T VARNUM; PROC CONTENTS DATA=NPA_T VARNUM; PROC CONTENTS DATA=GRAIL_T VARNUM; RUN;
PROC SORT DATA=CAT_T ; BY _LABEL_ _NAME_; PROC SORT DATA=SUBCAT_T ; BY _LABEL_ _NAME_; PROC SORT DATA=NPA_T ; BY _LABEL_ _NAME_; PROC SORT DATA=GRAIL_T ; BY _LABEL_ _NAME_; RUN;

* MERGE TRANSPOSED DATASETS;
DATA DEEPD.COMBO (DROP=_NAME_ _LABEL_ NRX_NPA_T TRX_NPA_T
		COMMUNICATION_PROGRAMS___CAMPAIG CUSTOMER_RESOURCES MEDICAL_EDUCATION OUTCOMES_RESEARCH_AND_PRICING SALES_TEAM_SUPPORT
		SAMPLES___COUPONS___VOUCHERS STRATEGIC_INPUT___MARKET_UNDERST GRANTS_AND_CONTRIBUTIONS TOTAL ADVERTISING_PROGRAMS
		MULTI_CHANNEL_CAMPAIGN_EXECUTION DISEASE_TREATMENT_RESOURCES PRODUCT_PROMOTIONAL_RESOURCES LECTURE_PROGRAMS MERCK_MEDICAL_FORUMS
		COUPONS___VOUCHERS PRODUCT_SAMPLES SAMPLES_DISTRIBUTION___FULFILLME PUBLIC_AFFAIRS_EXTERNAL_RELATION PAYER_POLICY_RESOURCES
		CONGRESS___EXHIBITS HEALTH_CARE_CONSUMER__HCC__PROGR SPEAKER_PREPARATION_FORUMS OUTCOMES_RESEARCH PRICING_AND_REIMBURSEMENT_SUPPOR
		LAUNCH_MEETINGS SALES_TEAM_SUPPORT_AND_LOGISTICS ADVISORY_BOARDS COMPETETIVE_INTELLIGENCE EXPERT_INPUT_FORUMS 
		MARKET_RESEARCH_ACTIVITIES NETWORK_STRATEGY__COP_ PROMOTIONAL_MATERIAL_DEPRECIATIO CHARITABLE_CONTRIBUTIONS GRANTS TOTAL_S
		OTHER NRX_GRL_C TRX_GRL_C DETAILS_C SAMPLES_C MNRX_VOLUME_C MTRX_VOLUME_C NRX_SHARE_C TRX_SHARE_C 
        I mval);
	FORMAT SAS_DATE YYMMD.;
	MERGE CAT_T SUBCAT_T (RENAME=(TOTAL=TOTAL_S))
		  NPA_T (RENAME=(NRX_VOLUME=NRX_NPA_T TRX_VOLUME=TRX_NPA_T)) 
		  GRAIL_T (RENAME=(NRX_VOLUME=NRX_GRL_C TRX_VOLUME=TRX_GRL_C DETAILS=DETAILS_C SAMPLES=SAMPLES_C 
                            MNRX_VOLUME=MNRX_VOLUME_C MTRX_VOLUME=MTRX_VOLUME_C NRX_SHARE=NRX_SHARE_C TRX_SHARE=TRX_SHARE_C));
	BY _LABEL_ _NAME_;
	ARRAY EXNAME {45} COMMUNICATION_PROGRAMS___CAMPAIG CUSTOMER_RESOURCES MEDICAL_EDUCATION OUTCOMES_RESEARCH_AND_PRICING SALES_TEAM_SUPPORT
					SAMPLES___COUPONS___VOUCHERS STRATEGIC_INPUT___MARKET_UNDERST GRANTS_AND_CONTRIBUTIONS ADVERTISING_PROGRAMS 
					MULTI_CHANNEL_CAMPAIGN_EXECUTION DISEASE_TREATMENT_RESOURCES PRODUCT_PROMOTIONAL_RESOURCES LECTURE_PROGRAMS 
					MERCK_MEDICAL_FORUMS COUPONS___VOUCHERS PRODUCT_SAMPLES SAMPLES_DISTRIBUTION___FULFILLME 
					PUBLIC_AFFAIRS_EXTERNAL_RELATION PAYER_POLICY_RESOURCES CONGRESS___EXHIBITS HEALTH_CARE_CONSUMER__HCC__PROGR
					SPEAKER_PREPARATION_FORUMS OUTCOMES_RESEARCH PRICING_AND_REIMBURSEMENT_SUPPOR LAUNCH_MEETINGS 
					SALES_TEAM_SUPPORT_AND_LOGISTICS ADVISORY_BOARDS COMPETETIVE_INTELLIGENCE EXPERT_INPUT_FORUMS 
					MARKET_RESEARCH_ACTIVITIES NETWORK_STRATEGY__COP_ PROMOTIONAL_MATERIAL_DEPRECIATIO
					CHARITABLE_CONTRIBUTIONS GRANTS TOTAL TOTAL_S OTHER DETAILS_C SAMPLES_C NRX_GRL_C TRX_GRL_C 
                    MNRX_VOLUME_C MTRX_VOLUME_C NRX_SHARE_C TRX_SHARE_C;
	ARRAY NWNAME {45} C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_4_OUTCOMES_RESEARCH_PRICING 
					C_7_SALES_TEAM_SUPPORT C_6_SAMPLES_COUPONS_VOUCHERS C_5_STRATEGIC_INPUT C_8_GRANTS_CONTRIBUTIONS 
					SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
					SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT 
					SC_2_PUBLIC_AFFAIRS_EXT_RLTNS SC_1_PAYER_POLICY_RESOURCES SC_3_CONGRESSES_EXHIBITS SC_3_HCC_PROGRAMS
					SC_3_SPEAKER_PREP_FORUMS SC_4_OUTCOMES_RESEARCH SC_4_PRICING_AND_REIMB_SUPPORT SC_7_LAUNCH_MEETINGS 
					SC_7_SALES_TEAM_SUPPORT SC_5_AD_BOARDS SC_5_COMPETITIVE_INTELLIGENCE SC_5_EXPERT_INPUT_FORUMS SC_5_MARKET_RESEARCH 
					SC_5_COP SC_1_PROMO_MATERIALS_DEPREC SC_8_CHARITABLE_CONTRIBUTIONS SC_8_GRANTS TOTAL_C TOTAL_SC OTHER_SC 
					DETAILS SAMPLES NRX_GRL TRX_GRL MNRX_VOLUME MTRX_VOLUME NRX_SHARE TRX_SHARE;

*TOTAL_C POPULATED ONLY FOR 2009 AND 2010...ELSE ZERO FOR EXCEL GRAPHING PURPOSES;
*TOTAL_SC POPULATED ONLY FOR 2009 AND 2010...ELSE ZERO FOR EXCEL GRAPHING PURPOSES;

* C_ PREFIX IS FOR CATEGORY;
* SC_ PREFIX IS FOR SUB-CATEGORY;
* OTHER _SC SUM OF SUB-CATEGORIES NOT EXPECTED TO EXERT DIRECT INFLUENCE ON RX VOLUME...COMPUTED IN EXCEL PRE-IMPORT;

	* RENAME TRANSPOSED VARIABLES AND CONVERT TO NUMERIC FROM CHARACTER;
	DO I=1 TO DIM(NWNAME);
		NWNAME(I)= EXNAME(I)*1;
	END;

	/*
	*CONVERT EXCEL DATE TO SAS DATE...EXCEL DATE USES 1900 BASE YEAR;
	SAS_DATE=_LABEL_-21916; 
	*/
	%MACRO getM(mstr);
      if &mstr. = "Jan" then mval = 1; if &mstr. = "Feb" then mval = 2;  if &mstr. = "Mar" then mval = 3;
	  if &mstr. = "Apr" then mval = 4; if &mstr. = "May" then mval = 5;  if &mstr. = "Jun" then mval = 6;
	  if &mstr. = "Jul" then mval = 7; if &mstr. = "Aug" then mval = 8;  if &mstr. = "Sep" then mval = 9;
	  if &mstr. = "Oct" then mval = 10; if &mstr. = "Nov" then mval = 11;  if &mstr. = "Dec" then mval = 12;
	%MEND;
	%getM(SUBSTR(_LABEL_,1,3));
	SAS_DATE=MDY(mval,1,2000+(SUBSTR(_LABEL_,5,2)*1));


	* CREATE SUM OF SUB-CATEGORIES EXPECTED TO EXERT DIRECT INFLUENCE ON RX VOLUME...OTHER SUB-CATEGORIES ARE RESEARCH/SUPPORT-TYPE;
	DIRECT_SC=SUM(OF SC_2_ADVERTISING_PROGRAMS	SC_2_MCM_CAMPAIGN_EXECUTION	SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES
			  SC_3_LECTURE_PROGRAMS	SC_3_MMFS SC_6_COUPONS_VOUCHERS	SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT);

	*COMPUTE TOTAL PROMO SPEND FOR 2011, 2012 & 2013 ELSE USE TOTAL_SC;
	IF SAS_DATE>='1-JAN-2011'D THEN TOTAL_PROMO=DIRECT_SC+OTHER_SC;
		ELSE TOTAL_PROMO=TOTAL_SC;

	* NPA DATA REPORTED IN THOUSANDS...RESCALE BELOW;
	NRX_NPA=NRX_NPA_T*1000;
	TRX_NPA=TRX_NPA_T*1000;

	* PREP FOR MATCHING WEIGHTS TO 2009/2010 OBS;
	IF SAS_DATE<'1-JAN-2011'D THEN MTCVAR=1; ELSE MTCVAR=0; 
RUN;

PROC PRINT DATA=DEEPD.COMBO (OBS=5); RUN;
PROC CONTENTS DATA=DEEPD.COMBO VARNUM; RUN;
PROC SORT DATA=DEEPD.COMBO; BY sas_date; RUN;

*EXPLORE VARIABILITY OF MONTHLY WEIGHTS BY PROMO TYPE FOR 2011...TO BE USED TO DERIVE 2009/2010 PROMO BY TYPE;
DATA MNTHLY_WGHTS_2011 (KEEP=SAS_DATE TOTAL_PROMO C_WGHT: SC_WGHT:);
	SET DEEPD.COMBO (WHERE=(SAS_DATE>='1-JAN-2011'D AND SAS_DATE<'1-JAN-2012'D));
	ARRAY PROMO {13}
		  C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		  SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		  SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT;
	ARRAY WGHT {13} 
		  C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		  SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL;
	DO I=1 TO DIM(PROMO);
		WGHT(I)= PROMO(I)/TOTAL_PROMO; 
	END;
	C_WGHT_SUM=SUM(OF C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS);
	SC_WGHT_SUM=SUM(OF SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  			   SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL);
RUN;
PROC TRANSPOSE DATA=MNTHLY_WGHTS_2011 OUT=TMNTHLY_WGHTS_2011; ID SAS_DATE;
	VAR C_WGHT_SUM SC_WGHT_SUM C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL; RUN;
PROC PRINT DATA=TMNTHLY_WGHTS_2011; RUN;

* CREATE WEIGHTS BY PROMOTION TYPE FOR ALL OF 2011 TO DERIVE 2009/2010 PROMOTION EXPENSE BY TYPE;
PROC MEANS NOPRINT DATA=DEEPD.COMBO (WHERE=(SAS_DATE>='1-JAN-2011'D AND SAS_DATE<'1-JAN-2012'D));
	VAR C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT TOTAL_PROMO DIRECT_SC;
	OUTPUT OUT=SUMS_2011 
	SUM=C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
		SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
		SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL SUM_TOTAL_PROMO SC_SUM_DIRECT;
	RUN;

DATA WEIGHTS_2011 (DROP=C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
						SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
						SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL
						_TYPE_ _FREQ_ I SUM_TOTAL_PROMO SC_SUM_DIRECT);
	SET SUMS_2011;
	ARRAY SUMS {14} 
		C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
		SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
		SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL SC_SUM_DIRECT;
	ARRAY WGHT {14} 
		C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL SC_WGHT_DIRECT;
	DO I=1 TO DIM(SUMS);
		WGHT(I)=SUMS(I)/SUM_TOTAL_PROMO; 
	END;
	MTCVAR=1;
RUN;

PROC SORT DATA=DEEPD.COMBO; BY MTCVAR SAS_DATE; RUN;
DATA DEEPD.INFERRED (KEEP=SAS_DATE CI_2_COMM_PROGRAMS_AND_CAMPAIGNS CI_1_CUSTOMER_RESOURCES CI_3_MEDICAL_EDUCATION CI_6_SAMPLES_COUPONS_VOUCHERS 
		  SCI_2_ADVERTISING_PROGRAMS SCI_2_MCM_CAMPAIGN_EXECUTION SCI_1_DISEASE_TREATMENT_RSCRS SCI_1_PRODUCT_PROMO_RESOURCES 
		  SCI_3_LECTURE_PROGRAMS SCI_3_MMFS SCI_6_COUPONS_VOUCHERS SCI_6_PRODUCT_SAMPLES SCI_6_SAMPLES_DIST_FULFILLMENT SCI_DIRECT
		  TOTAL_PROMO DETAILS SAMPLES NRX: TRX:);
	MERGE DEEPD.COMBO WEIGHTS_2011;
	BY MTCVAR;
	ARRAY PROMO {14}
		  C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		  SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		  SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT DIRECT_SC;
	ARRAY WGHT {14} 
		  C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		  SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL SC_WGHT_DIRECT;
	ARRAY NEWP {14}
		  CI_2_COMM_PROGRAMS_AND_CAMPAIGNS CI_1_CUSTOMER_RESOURCES CI_3_MEDICAL_EDUCATION CI_6_SAMPLES_COUPONS_VOUCHERS 
		  SCI_2_ADVERTISING_PROGRAMS SCI_2_MCM_CAMPAIGN_EXECUTION SCI_1_DISEASE_TREATMENT_RSCRS SCI_1_PRODUCT_PROMO_RESOURCES 
		  SCI_3_LECTURE_PROGRAMS SCI_3_MMFS SCI_6_COUPONS_VOUCHERS SCI_6_PRODUCT_SAMPLES SCI_6_SAMPLES_DIST_FULFILLMENT SCI_DIRECT;
	DO I=1 TO DIM(PROMO);
		IF MTCVAR=1 THEN NEWP(I)=TOTAL_PROMO*WGHT(I);
			ELSE NEWP(I)=PROMO(I);
		IF NEWP(I)=. THEN NEWP(I)=0;
	END;
RUN;
PROC SORT DATA=DEEPD.INFERRED; BY SAS_DATE; RUN;
