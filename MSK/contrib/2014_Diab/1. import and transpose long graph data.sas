LIBNAME DEEPD "\\WPUSHH01\DINFOPLN\MARKETING MIX PI\INVOPT\P2 2014 AB\PROMO\DIABETES DEEP DIVE\SAS WORK";

******************************************************************************************************************************************;
* IMPORT PROMOTION SPEND, DIRECT SELLING AND RX VOLUME DATA FROM EXCEL "LONG GRAPHS" SPREADSHEET. TRANSPOSE, RENAME AND WEIGHT 2009/10.   ;
******************************************************************************************************************************************;

* BRING EXCEL DATA INTO SAS FROM FOUR SPREADSHEETS;
%MACRO IMPEM (FNAME,RNAME);
	PROC IMPORT OUT= WORK.&FNAME 
	DATAFILE= "\\WPUSHH01\DINFOPLN\MARKETING MIX PI\INVOPT\P2 2014 AB\PROMO\DIABETES DEEP DIVE\DIABETES PROMO DETAIL 2009-2013 LONG GRAPHS.XLS"
	DBMS=EXCEL REPLACE; RANGE="&RNAME"; GETNAMES=YES; MIXED=NO; SCANTEXT=YES; USEDATE=YES; SCANTIME=YES; RUN;
%MEND;
%IMPEM (CAT,CATEGORY$); 					*PROMOTION SPEND BY CATEGORY;
%IMPEM (SUBCAT,SUB-CATEGORY$); 				*PROMOTION SPEND BY SUB-CATEGORY;
%IMPEM (NPA,RX VOLUME (NPA)$); 				*RX VOLUME FROM BADS DRIVE;
%IMPEM (GRAIL,RX VOLUME & PROMO (GRAIL)$); 	*RX VOLUME AND PROMOTION FROM GRAIL;

PROC CONTENTS DATA=CAT VARNUM; PROC CONTENTS DATA=SUBCAT VARNUM; PROC CONTENTS DATA=NPA VARNUM; PROC CONTENTS DATA=GRAIL VARNUM; RUN;

* TRANSPOSE...VAR NAMES ARE EXCEL DATES WITH FIRST CHARACTER REPLACED BY UNDERSCORE (EXCEL TO SAS DATE TRANSFORMATION IN NEXT DATA STEP);
* VAR USED TO FORCE TRANSPOSITION OF ALL MONTHS IN EACH FILE DESPITE MISSING VALUES...MISSING VALUES RESULT IN VARS AS CHARACTER FORMAT;
%MACRO TRANEM (FNAME, VTYPE);
	PROC TRANSPOSE DATA=&FNAME OUT=&FNAME._T; ID &VTYPE; 
	 VAR _9814 _9845 _9873 _9904 _9934 _9965 _9995 _0026 _0057 _0087 _0118 _0148 
		 _0179 _0210 _0238 _0269 _0299 _0330 _0360 _0391 _0422 _0452 _0483 _0513 
		 _0544 _0575 _0603 _0634 _0664 _0695 _0725 _0756 _0787 _0817 _0848 _0878 
		 _0909 _0940 _0969 _1000 _1030 _1061 _1091 _1122 _1153 _1183 _1214 _1244 
		 _1275 _1306 _1334 _1365 _1395 _1426 _1456 _1487 _1518 _1548 _1579 _1609;
%MEND;
%TRANEM(CAT,CATEGORY);
%TRANEM(SUBCAT,SUB_CATEGORY_LABEL);
%TRANEM(NPA,REC_TYPE);
%TRANEM(GRAIL,REC_TYPE); RUN;

PROC CONTENTS DATA=CAT_T VARNUM; PROC CONTENTS DATA=SUBCAT_T VARNUM; PROC CONTENTS DATA=NPA_T VARNUM; PROC CONTENTS DATA=GRAIL_T VARNUM; RUN;
PROC SORT DATA=CAT_T ; BY _LABEL_ _NAME_; PROC SORT DATA=SUBCAT_T ; BY _LABEL_ _NAME_; PROC SORT DATA=NPA_T ; BY _LABEL_ _NAME_; PROC SORT DATA=GRAIL_T ; BY _LABEL_ _NAME_; RUN;

* MERGE TRANSPOSED DATASETS;
DATA DEEPD.COMBO (DROP=_NAME_ _LABEL_ NRX_NPA_T TRX_NPA_T
		COMMUNICATION_PROGRAMS___CAMPAIG CUSTOMER_RESOURCES MEDICAL_EDUCATION OUTCOMES_RESEARCH_AND_PRICING SALES_TEAM_SUPPORT
		SAMPLES___COUPONS___VOUCHERS STRATEGIC_INPUT___MARKET_UNDERST GRANTS_AND_CONTRIBUTIONS TOTAL ADVERTISING_PROGRAMS
		MULTI_CHANNEL_CAMPAIGN_EXECUTION DISEASE_TREATMENT_RESOURCES PRODUCT_PROMOTIONAL_RESOURCES LECTURE_PROGRAMS MERCK_MEDICAL_FORUMS
		COUPONS___VOUCHERS PRODUCT_SAMPLES SAMPLES_DISTRIBUTION___FULFILLME PUBLIC_AFFAIRS_EXTERNAL_RELATION PAYER_POLICY_RESOURCES
		CONGRESS___EXHIBITS HEALTH_CARE_CONSUMER__HCC__PROGR SPEAKER_PREPARATION_FORUMS OUTCOMES_RESEARCH PRICING_AND_REIMBURSEMENT_SUPPOR
		LAUNCH_MEETINGS SALES_TEAM_SUPPORT_AND_LOGISTICS ADVISORY_BOARDS COMPETETIVE_INTELLIGENCE EXPERT_INPUT_FORUMS 
		MARKET_RESEARCH_ACTIVITIES NETWORK_STRATEGY__COP_ PROMOTIONAL_MATERIAL_DEPRECIATIO CHARITABLE_CONTRIBUTIONS GRANTS TOTAL_S
		OTHER NRX_GRL_C TRX_GRL_C DETAILS_C SAMPLES_C);
	FORMAT SAS_DATE YYMMD.;
	MERGE CAT_T SUBCAT_T (RENAME=(TOTAL=TOTAL_S))
		  NPA_T (RENAME=(NRX_VOLUME=NRX_NPA_T TRX_VOLUME=TRX_NPA_T)) 
		  GRAIL_T (RENAME=(NRX_VOLUME=NRX_GRL_C TRX_VOLUME=TRX_GRL_C DETAILS=DETAILS_C SAMPLES=SAMPLES_C));
	BY _LABEL_ _NAME_;
	SAS_DATE=_LABEL_-21916; *CONVERT EXCEL DATE TO SAS DATE...EXCEL DATE USES 1900 BASE YEAR;
	* RENAME EACH TRANSPOSED VARIABLE AND CONVERT TO NUMERIC FROM CHARACTER;
		C_2_COMM_PROGRAMS_AND_CAMPAIGNS=COMMUNICATION_PROGRAMS___CAMPAIG*1; 	* C_ PREFIX IS FOR CATEGORY;
		C_1_CUSTOMER_RESOURCES=CUSTOMER_RESOURCES*1;
		C_3_MEDICAL_EDUCATION=MEDICAL_EDUCATION*1;
		C_4_OUTCOMES_RESEARCH_PRICING=OUTCOMES_RESEARCH_AND_PRICING*1;
		C_7_SALES_TEAM_SUPPORT=SALES_TEAM_SUPPORT*1;
		C_6_SAMPLES_COUPONS_VOUCHERS=SAMPLES___COUPONS___VOUCHERS*1;
		C_5_STRATEGIC_INPUT=STRATEGIC_INPUT___MARKET_UNDERST*1;
		C_8_GRANTS_CONTRIBUTIONS=GRANTS_AND_CONTRIBUTIONS*1;
		SC_2_ADVERTISING_PROGRAMS=ADVERTISING_PROGRAMS*1;						* SC_ PREFIX IS FOR SUB-CATEGORY;
		SC_2_MCM_CAMPAIGN_EXECUTION=MULTI_CHANNEL_CAMPAIGN_EXECUTION*1;
		SC_1_DISEASE_TREATMENT_RSCRS=DISEASE_TREATMENT_RESOURCES*1;
		SC_1_PRODUCT_PROMO_RESOURCES=PRODUCT_PROMOTIONAL_RESOURCES*1;
		SC_3_LECTURE_PROGRAMS=LECTURE_PROGRAMS*1;
		SC_3_MMFS=MERCK_MEDICAL_FORUMS*1;
		SC_6_COUPONS_VOUCHERS=COUPONS___VOUCHERS*1;
		SC_6_PRODUCT_SAMPLES=PRODUCT_SAMPLES*1;
		SC_6_SAMPLES_DIST_FULFILLMENT=SAMPLES_DISTRIBUTION___FULFILLME*1;
		SC_2_PUBLIC_AFFAIRS_EXT_RLTNS=PUBLIC_AFFAIRS_EXTERNAL_RELATION*1;
		SC_1_PAYER_POLICY_RESOURCES=PAYER_POLICY_RESOURCES*1;
		SC_3_CONGRESSES_EXHIBITS=CONGRESS___EXHIBITS*1;
		SC_3_HCC_PROGRAMS=HEALTH_CARE_CONSUMER__HCC__PROGR*1;
		SC_3_SPEAKER_PREP_FORUMS=SPEAKER_PREPARATION_FORUMS*1;
		SC_4_OUTCOMES_RESEARCH=OUTCOMES_RESEARCH*1;
		SC_4_PRICING_AND_REIMB_SUPPORT=PRICING_AND_REIMBURSEMENT_SUPPOR*1;
		SC_7_LAUNCH_MEETINGS=LAUNCH_MEETINGS*1;
		SC_7_SALES_TEAM_SUPPORT=SALES_TEAM_SUPPORT_AND_LOGISTICS*1;
		SC_5_AD_BOARDS=ADVISORY_BOARDS*1;
		SC_5_COMPETITIVE_INTELLIGENCE=COMPETETIVE_INTELLIGENCE*1;
		SC_5_EXPERT_INPUT_FORUMS=EXPERT_INPUT_FORUMS*1;
		SC_5_MARKET_RESEARCH=MARKET_RESEARCH_ACTIVITIES*1;
		SC_5_COP=NETWORK_STRATEGY__COP_*1;
		SC_1_PROMO_MATERIALS_DEPREC=PROMOTIONAL_MATERIAL_DEPRECIATIO*1;
		SC_8_CHARITABLE_CONTRIBUTIONS=CHARITABLE_CONTRIBUTIONS*1;
		SC_8_GRANTS=GRANTS*1;
		TOTAL_C=TOTAL*1; *POPULATED ONLY FOR 2009 AND 2010...ELSE ZERO FOR EXCEL GRAPHING PURPOSES;
		TOTAL_SC=TOTAL_S*1; *POPULATED ONLY FOR 2009 AND 2010...ELSE ZERO FOR EXCEL GRAPHING PURPOSES;
		* CREATE SUM OF SUB-CATEGORIES EXPECTED TO EXERT DIRECT INFLUENCE ON RX VOLUME...OTHER SUB-CATEGORIES ARE RESEARCH/SUPPORT-TYPE;
		DIRECT_SC=SUM(OF SC_2_ADVERTISING_PROGRAMS	SC_2_MCM_CAMPAIGN_EXECUTION	SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES
				  SC_3_LECTURE_PROGRAMS	SC_3_MMFS SC_6_COUPONS_VOUCHERS	SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT);
		OTHER_SC=OTHER*1; *SUM OF SUB-CATEGORIES NOT EXPECTED TO EXERT DIRECT INFLUENCE ON RX VOLUME...COMPUTED IN EXCEL PRE-IMPORT;
		*COMPUTE TOTAL PROMO SPEND FOR 2011, 2012 & 2013 ELSE USE TOTAL_SC;
		IF SAS_DATE>='1-JAN-2011'D THEN TOTAL_PROMO=DIRECT_SC+OTHER_SC;
			ELSE TOTAL_PROMO=TOTAL_SC;
		DETAILS=DETAILS_C*1;
		SAMPLES=SAMPLES_C*1;
		NRX_GRL=NRX_GRL_C*1;
		TRX_GRL=TRX_GRL_C*1;
	* NPA DATA REPORTED IN THOUSANDS...RESCALE BELOW;
	NRX_NPA=NRX_NPA_T*1000;
	TRX_NPA=TRX_NPA_T*1000;
	IF SAS_DATE<'1-JAN-2011'D THEN MTCVAR=1; ELSE MTCVAR=0; * PREP FOR MATCHING WEIGHTS TO 2009/2010 OBS;
RUN;

PROC PRINT DATA=DEEPD.COMBO (OBS=5); RUN;
PROC CONTENTS DATA=DEEPD.COMBO VARNUM; RUN;

*EXPLORE VARIABILITY OF MONTHLY WEIGHTS BY PROMO TYPE FOR 2011...TO BE USED TO DERIVE 2009/2010 PROMO BY TYPE;
DATA MNTHLY_WGHTS_2011 (KEEP=SAS_DATE TOTAL_PROMO C_WGHT: SC_WGHT:);
	SET DEEPD.COMBO (WHERE=(SAS_DATE>='1-JAN-2011'D AND SAS_DATE<'1-JAN-2012'D));
	ARRAY PROMO {13}
		  C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		  SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		  SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT;
	ARRAY WGHT {13} 
		  C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		  SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL;
	DO I=1 TO DIM(PROMO);
		WGHT(I)= PROMO(I)/TOTAL_PROMO; 
	END;
	C_WGHT_SUM=SUM(OF C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS);
	SC_WGHT_SUM=SUM(OF SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  			   SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL);
RUN;
PROC TRANSPOSE DATA=MNTHLY_WGHTS_2011 OUT=TMNTHLY_WGHTS_2011; ID SAS_DATE;
	VAR C_WGHT_SUM SC_WGHT_SUM C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL; RUN;
PROC PRINT DATA=TMNTHLY_WGHTS_2011; RUN;

* CREATE WEIGHTS BY PROMOTION TYPE FOR ALL OF 2011 TO DERIVE 2009/2010 PROMOTION EXPENSE BY TYPE;
PROC MEANS NOPRINT DATA=DEEPD.COMBO (WHERE=(SAS_DATE>='1-JAN-2011'D AND SAS_DATE<'1-JAN-2012'D));
	VAR C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT TOTAL_PROMO DIRECT_SC;
	OUTPUT OUT=SUMS_2011 
	SUM=C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
		SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
		SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL SUM_TOTAL_PROMO SC_SUM_DIRECT;
	RUN;

DATA WEIGHTS_2011 (DROP=C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
						SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
						SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL
						_TYPE_ _FREQ_ I SUM_TOTAL_PROMO SC_SUM_DIRECT);
	SET SUMS_2011;
	ARRAY SUMS {14} 
		C_SUM_2_CPCS C_SUM_1_CRS C_SUM_3_ME C_SUM_6_SCVS 
		SC_SUM_2_AP SC_SUM_2_MCM_CE SC_SUM_1_DTRS SC_SUM_1_PPRS 
		SC_SUM_3_LPS SC_SUM_3_MMFS SC_SUM_6_CV SC_SUM_6_PS SC_SUM_6_SAMFUL SC_SUM_DIRECT;
	ARRAY WGHT {14} 
		C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL SC_WGHT_DIRECT;
	DO I=1 TO DIM(SUMS);
		WGHT(I)=SUMS(I)/SUM_TOTAL_PROMO; 
	END;
	MTCVAR=1;
RUN;

PROC SORT DATA=DEEPD.COMBO; BY MTCVAR SAS_DATE; RUN;
DATA DEEPD.INFERRED (KEEP=SAS_DATE CI_2_COMM_PROGRAMS_AND_CAMPAIGNS CI_1_CUSTOMER_RESOURCES CI_3_MEDICAL_EDUCATION CI_6_SAMPLES_COUPONS_VOUCHERS 
		  SCI_2_ADVERTISING_PROGRAMS SCI_2_MCM_CAMPAIGN_EXECUTION SCI_1_DISEASE_TREATMENT_RSCRS SCI_1_PRODUCT_PROMO_RESOURCES 
		  SCI_3_LECTURE_PROGRAMS SCI_3_MMFS SCI_6_COUPONS_VOUCHERS SCI_6_PRODUCT_SAMPLES SCI_6_SAMPLES_DIST_FULFILLMENT SCI_DIRECT
		  TOTAL_PROMO DETAILS SAMPLES NRX: TRX:);
	MERGE DEEPD.COMBO WEIGHTS_2011;
	BY MTCVAR;
	ARRAY PROMO {14}
		  C_2_COMM_PROGRAMS_AND_CAMPAIGNS C_1_CUSTOMER_RESOURCES C_3_MEDICAL_EDUCATION C_6_SAMPLES_COUPONS_VOUCHERS 
		  SC_2_ADVERTISING_PROGRAMS SC_2_MCM_CAMPAIGN_EXECUTION SC_1_DISEASE_TREATMENT_RSCRS SC_1_PRODUCT_PROMO_RESOURCES 
		  SC_3_LECTURE_PROGRAMS SC_3_MMFS SC_6_COUPONS_VOUCHERS SC_6_PRODUCT_SAMPLES SC_6_SAMPLES_DIST_FULFILLMENT DIRECT_SC;
	ARRAY WGHT {14} 
		  C_WGHT_2_CPCS C_WGHT_1_CRS C_WGHT_3_ME C_WGHT_6_SCVS 
		  SC_WGHT_2_AP SC_WGHT_2_MCM_CE SC_WGHT_1_DTRS SC_WGHT_1_PPRS 
		  SC_WGHT_3_LPS SC_WGHT_3_MMFS SC_WGHT_6_CV SC_WGHT_6_PS SC_WGHT_6_SAMFUL SC_WGHT_DIRECT;
	ARRAY NEWP {14}
		  CI_2_COMM_PROGRAMS_AND_CAMPAIGNS CI_1_CUSTOMER_RESOURCES CI_3_MEDICAL_EDUCATION CI_6_SAMPLES_COUPONS_VOUCHERS 
		  SCI_2_ADVERTISING_PROGRAMS SCI_2_MCM_CAMPAIGN_EXECUTION SCI_1_DISEASE_TREATMENT_RSCRS SCI_1_PRODUCT_PROMO_RESOURCES 
		  SCI_3_LECTURE_PROGRAMS SCI_3_MMFS SCI_6_COUPONS_VOUCHERS SCI_6_PRODUCT_SAMPLES SCI_6_SAMPLES_DIST_FULFILLMENT SCI_DIRECT;
	DO I=1 TO DIM(PROMO);
		IF MTCVAR=1 THEN NEWP(I)=TOTAL_PROMO*WGHT(I);
			ELSE NEWP(I)=PROMO(I);
		IF NEWP(I)=. THEN NEWP(I)=0;
	END;
RUN;
PROC SORT DATA=DEEPD.INFERRED; BY SAS_DATE; RUN;
