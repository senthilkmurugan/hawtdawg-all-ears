PROC PRINTTO LOG="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\PGM1.LOG" NEW
			PRINT="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\PGM1.LST" NEW;

** PROGRAM COMBINES GRAIL AND HEL DATA;

OPTIONS NOCENTER MPRINT COMPRESS = YES;
%LET PRODUCT=ZVTTOT; RUN;
%LET PRODLABEL=ZVTTOT; RUN;  /* VALUE IS SAME AS PRODUCT FOR ALL BRANDS EXCEPT ASMANEX */
                                    /* ASMANEX IS ASMANEX-DULERA OR ASMANEX-SING */ 
*LIBNAME MMF "\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\&PRODLABEL"; 
LIBNAME MMF "C:\E\MMF 2012\&PRODLABEL"; 
LIBNAME GRAIL "\\USWPUS52014\DATA\GRAIL\&PRODUCT";
*LIBNAME ATT '\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\MMF';
LIBNAME ATT 'C:\E\MMF 2012';
LIBNAME TARG '\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\TARGETS & SPEC';
TITLE ' ';  RUN;

**********************;
*** SET MACRO VARS ***;
**********************;
%LET GRAILDAT=&PRODUCT._GRAIL1201; 
%LET ATTENDAT=MFATND_DEC10NOV11;
%LET SPKRDAT=MFSPKR_DEC10NOV11;
%LET COVDSN=FEB10JAN12;
%LET TARGDSN=TARGETS_2011;

*%LET M=5; * DULERA - MONTHS OF HELDATA;
*%LET MONARRAY=(7, 8, 9, 10, 11);    ** CALENDAR MONTHS;
*%LET YRARRAY =(2011, 2011, 2011, 2011, 2011);

*%LET NUMARRAY=(7, 6, 5, 4, 3);    **GRAIL MONTHS;
*%LET QTRARRAY=('2011Q3' '2011Q3' '2011Q3' '2011Q4' '2011Q4');

%LET M=12; * OTHER BRANDS - MONTHS OF HELDATA;
%LET MONARRAY=(12, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11);    ** CALENDAR MONTHS;
%LET YRARRAY =(2010, 2011, 2011, 2011, 2011, 2011, 2011, 2011, 2011, 2011, 2011, 2011);

%LET NUMARRAY=(14, 13, 12, 11, 10, 9, 8, 7, 6, 5, 4, 3);    **GRAIL MONTHS;
%LET QTRARRAY=('2010Q4' '2011Q1' '2011Q1' '2011Q1' '2011Q2' '2011Q2' 
               '2011Q2' '2011Q3' '2011Q3' '2011Q3' '2011Q4' '2011Q4' );

RUN;


*** REDUCE HEL DATA FOR: DATE, HELCODE, PRODUCT;
TITLE1 "MMF AND EMF ANALYSIS - RUN &SYSDATE - PRODUCT=&PRODUCT"; RUN;
DATA MMF.HEL_SUBSET (DROP=I MON1-MON&M YR1-YR&M NUM1-NUM&M QTR1-QTR&M);
SET ATT.&ATTENDAT(DROP=QTR WHERE=(PRODUCT="&PRODLABEL"));
ARRAY MON (&M)  MON1-MON&M &MONARRAY;
ARRAY YR  (&M)  YR1-YR&M   &YRARRAY;
ARRAY NUM (&M)  NUM1-NUM&M &NUMARRAY;
ARRAY QRT (&M) $6 QTR1-QTR&M &QTRARRAY;
IF HELCODE IN ('EMF','ODEMF') THEN DELETE;  /* REMOVE ALL EMF FROM ANALYSIS */

FOUND='N';
DO I=1 TO &M UNTIL (FOUND='Y');
  IF (CALLMM=MON(I) AND CALLYY=YR(I)) THEN DO;
    MONTH=NUM(I); QTR=QRT(I); FOUND='Y';
  END;
END;
IF FOUND='Y';

IF IMEDREG IN ('19', '86', '99') THEN HSAIND=1;  /* IDENTIFY HSA PROGRAMS AND CARRY THROUGH */
   ELSE HSAIND=0;
       /* SPOKE WITH GEEM TEAM  - BETTER TO USE PROGRAM OWNER REGION THAN HSA TALK TITLES TO IDENTIFY HSA PROGRAMS */
       /* For 2011, 19-NEURO-HIV-OPH, 86-MVD, 99-CV-LIPID-RESP */
       /* For Jan10, 19-RESP-HIVID-INSOMNIA-NEURO, 81-HIV, 86-MVD, 99-CV, A4-CM MSP */
       /* For Dec10, 19-HIVID-INSOMNIA-NEURO-OPH, 81-HIV, 86-MVD, 99-CV/RESP, A4-CM MSP */
       /* FOR 2009 HSA REGIONS ARE: 19-RESP-HIV-INSOMNIA ; 81-HIV ; 86-MVD ; 99-CV ; A4-CM MSP */
  
RUN;

TITLE2 'PGM1 - HEL_SUBSET: PROMOTION DATA REDUCED FOR DATE, HELCODE';
PROC FREQ DATA=MMF.HEL_SUBSET;
TABLES MONTH*CALLYY*CALLMM*QTR HELCODE HELVENUE IMEDREG HSAIND HSAIND*HELCODE / LIST MISSING;
RUN;

PROC PRINT DATA=MMF.HEL_SUBSET(OBS=10); RUN;

*** MERGE HEL & GRAIL - REMOVING SPEAKERS FROM ANY DOC IN GRAIL DATASET;
*** MERGE ON TARGET TYPE & PROGRAM COUNTS FROM IMED TO REPLACE SFP;
PROC SORT DATA=MMF.HEL_SUBSET; BY IMSDRNUM; RUN;
PROC SORT DATA=TARG.&TARGDSN (KEEP=IMSDRNUM &PRODUCT WHERE=(&PRODUCT=1)) OUT=TARGETS NODUPKEY; BY IMSDRNUM; RUN;
PROC SORT DATA=ATT.PGMCNTS_&COVDSN (KEEP=IMSDRNUM PRODUCT PGMCNT1-PGMCNT24 WHERE=(PRODUCT="&PRODLABEL") 
               RENAME=(PGMCNT1-PGMCNT24=SFP1-SFP24)) OUT=PGMCNT NODUPKEY; BY IMSDRNUM; RUN;

DATA COMBINE;
LENGTH CAT CAT2 CAT3 $2;
MERGE MMF.HEL_SUBSET(IN=A) 
      GRAIL.&GRAILDAT. (IN=B RENAME=(CAT=RATING SPEC=OSPEC SFP1-SFP24=GRLSFP1-GRLSFP24) DROP=HODET: HOMFP: OFDET: OFMFP: OTDET: OTMFP: SPDET: SPMFP:)  
      TARGETS(IN=T)
      PGMCNT
      ATT.&SPKRDAT(IN=S KEEP=SPKIMSREF RENAME=(SPKIMSREF=IMSDRNUM));   
BY IMSDRNUM;
IF A OR B;
INHEL=A; INGRAIL=B; INSPEAKER=S; 

ARRAY SFP(24) SFP1-SFP24;  /* THESE ARE NOW ACTUAL ATTENDANCE COUNTS AND NOT SFP DETAILS FRON GRAIL */
DO I=1 TO 24;
  IF SFP(I)=. THEN SFP(I)=0;
END;

IF T THEN TARGET=1; ELSE TARGET=0;

* RECEIVED FROM DANIELLE - SALES FORCE PLANNING;
* NO LONGER CREATING NP-PA AND OTHER BUCKET AS OF PP09;
* PP12 CHANGED RESPCOMB PEDS FROM SP TO PCP;
IF "&PRODLABEL"='RESPCOMB' AND OSPEC IN ('A' 'AI' 'PDA' 'PDP' 'PUD' 'OTO' 'PDO') THEN SPEC='SP';
ELSE IF "&PRODLABEL" IN ('DULERA','ASMANEX-DULERA','ASMANEX-SING') AND OSPEC IN ('A' 'AI' 'PDA' 'PDP' 'PUD') THEN SPEC='SP';
ELSE IF "&PRODLABEL"='NASONEX' AND OSPEC IN ('A' 'AI' 'PDA' 'PDO' 'OTO') THEN SPEC='SP';
ELSE IF "&PRODLABEL" IN ('JANTOT','ZVTTOT') AND OSPEC IN ('CD','CDS','IC','ICE','VM','VS','DIA','END','NEP') THEN SPEC='SP';
ELSE IF "&PRODLABEL"='SAPHRIS' AND OSPEC IN ('CN' 'N' 'NS' 'P' 'PYG' 'PYN' 'PFP') THEN SPEC = 'SP';
ELSE IF "&PRODLABEL"='NUVARING' AND OSPEC IN ('GO' 'GY' 'GYN' 'MW' 'OB' 'OBG' 'OBS' 'OC' 'OCC' 'OG' 'SG' 'DA' 'NRP' 'NU' 'PHA') THEN SPEC = 'SP';
ELSE SPEC='PC'; 

IF SUBSTR(RATING,1,1) IN ( 'B' 'C') THEN CAT=SUBSTR(RATING,1,1);
  ELSE CAT=RATING;
CAT2=SUBSTR(CAT,1,1);
IF CAT2 IN ( 'C' 'D') THEN CAT3='CD'; ELSE CAT3=CAT2;
CATFOUND=(CAT NE '  ');  /* SOME BRANDS HAVE NOT-RATED DOCS ON GRAIL THAT CAN BE REMOVED*/

* IMED REGN/DIST/TERR ARE FROM HELDATA & ARE SALES FORCE SPECIFIC;
* RDT1 (PORT1-SINGULAIR,DULERA,ASMANEX,NASONEX) AND RDT2 (PORT2-MAXALT,JANTOT,ZVTTOT,C/H) ARE FROM GRAIL;
IF "&PRODLABEL" IN ('RESPCOMB','DULERA','ASMANEX-DULERA','ASMANEX-SING','NASONEX') THEN USCORDT=RDT1;
   ELSE USCORDT=RDT2;
IF USCORDT=RDT1 THEN CHECK1=1; ELSE CHECK1=0;
IF USCORDT=RDT2 THEN CHECK2=1; ELSE CHECK2=0;
USCOREGN=SUBSTR(USCORDT,1,2);
IF USCOREGN IN ('J1','J2','J3','J4','J5','J6','J7','J8','J9') THEN USCOCOG='NE';
ELSE IF USCOREGN IN ('S1','S2','S3','S4','S5','S6','S7','S8','S9') THEN USCOCOG='SE';
ELSE IF USCOREGN IN ('K1','K2','K3','K4','K5','K6','K7','K8','K9') THEN USCOCOG='NC';
ELSE IF USCOREGN IN ('W1','W2','W3','W4','W5','W6','W7','W8','W9') THEN USCOCOG='WE';
REGFOUND=(USCOREGN NOT IN (' ','99'));

RUN;

TITLE2 'PGM1 - PROMO & GRAIL MERGE: CHECK FOR PRESCRIBER TYPE';
PROC FREQ DATA=COMBINE;
TABLES INHEL*INGRAIL INHEL*INGRAIL*INSPEAKER*REGFOUND GRPCD REGFOUND*GRPCD SPEC SPEC*OSPEC TARGET CHECK1 CHECK2 / LIST MISSING;
RUN;

TITLE2 'PGM1 - PROMO & GRAIL MERGE: CHECK FOR MISSING USCOREGN';
PROC FREQ DATA=COMBINE;
WHERE REGFOUND=0;
TABLES INHEL USCOREGN / LIST MISSING;
RUN;

PROC CONTENTS DATA=COMBINE;  RUN;


* REMOVE SPEAKERS FROM CONTROL - ALREADY EXCLUDED FROM ATTENDEES;
* REMOVE RECORDS WITH MISSING DATA;
DATA MMF.SFP_INPUTSET MMF.HEL_DROPS;
     SET COMBINE (DROP=I CHECK1 CHECK2 RDT1 RDT2);
IF INGRAIL AND (REGFOUND) AND (CATFOUND) AND (NOT INSPEAKER) THEN DO;
  DOCID+1; OUTPUT MMF.SFP_INPUTSET;
  END;
ELSE IF INHEL THEN OUTPUT MMF.HEL_DROPS;
RUN;

*** PROCS;
TITLE2 'PGM1 - SFP_INPUTSET: GRAIL & PROMO COMBINED AND NO USCOREGN REMOVED';
PROC FREQ DATA=MMF.SFP_INPUTSET;
  TABLES INHEL*INGRAIL*REGFOUND*CATFOUND USCOCOG USCOREGN SPEC CAT: QTR HELCODE*(TALKTITLE1 TALKTITLE2) / LIST;
RUN;

PROC FREQ DATA=MMF.SFP_INPUTSET; TABLES INHEL*(USCOREGN SPEC CAT:) / NOCOL NOPERCENT MISSING;
PROC MEANS DATA=MMF.SFP_INPUTSET MAXDEC=2;
RUN;
PROC MEANS DATA=MMF.SFP_INPUTSET SUM; VAR GRLSFP1-GRLSFP24 SFP1-SFP24;  RUN;


TITLE2 'PGM1 - HEL_DROPS: PROMO DATA WITH NO USCOREGN OR INSPEAKER OR NOT INGRAIL';
PROC FREQ DATA=MMF.HEL_DROPS;
TABLES INHEL*INGRAIL*INSPEAKER*REGFOUND*CATFOUND / LIST MISSING;
RUN;
PROC PRINT DATA=MMF.HEL_DROPS(OBS=20);
VAR IMSDRNUM PGMID HELCODE CALLMM DOCMENO OSPEC
INHEL INGRAIL INSPEAKER REGFOUND CAT:;
RUN;

PROC PRINTTO;
QUIT;
