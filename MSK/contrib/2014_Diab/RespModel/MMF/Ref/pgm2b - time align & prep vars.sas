*PROC PRINTTO LOG="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\PGM2b.LOG" NEW
			PRINT="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\PGM2b.LST" NEW;
* APPEND HEL AND GRAIL DATA BACK ON TEST AND CANDIDATE DOCS;
* TIME ALIGN & PREP VARIABLES FOR THE MODEL;

* THIS PROGRAM ASSINGS CLUSTERS TO REGIONS - UPDATED FOR 2013 ANNUAL BUDGET;

TITLE1 'PGM2B - APPEND HEL & GRAIL AND TIME ALIGN & PREP VARS';
OPTIONS NOCENTER MPRINT;
*LIBNAME MMF '\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\MMF\ZVTTOT';
LIBNAME MMF 'C:\E\MMF 2012\ZVTTOT';
RUN;

/*** DULERA ONLY;
%LET MONUM=24;            ** # OF MONTHS FOR GRAIL VARIABLES (SOMETIMES ONLY DOWNLOAD 12);  
%LET LATEST_HELMONTH=7;   ** EARLIEST MONTH OF ATTENDANCE IN TERMS OF GRAIL MONTHS;
*/

*** OTHER BRANDS;
%LET MONUM=24;            ** # OF MONTHS FOR GRAIL VARIABLES (SOMETIMES ONLY DOWNLOAD 12);  
%LET LATEST_HELMONTH=14;  ** EARLIEST MONTH OF ATTENDANCE IN TERMS OF GRAIL MONTHS;

%LET CATVAR=CAT;          ** USE CAT FOR ALL PRODUCTS EXCEPT COSOPT - CAT2;
*** MACRO VARS FOR INDEPENDENT MODEL VARS;
*** FULL MODEL IS THE TRADITIONAL MODEL FOR ROI CALCULATION;
*** DTL MODEL MEASURES IMPACT OF ADDITIONAL DETAILS AS RESULT OF EVENT;
%LET NORMFULL=PROD_PREMEAN CLASS_PREMEAN SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
%LET NORMDTL= PROD_PREMEAN CLASS_PREMEAN SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN;
%LET POIFULL= PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
%LET POIDTL=  PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN;
RUN;


*** ADD BACK HELINFO & CREATE SEPARATE ROWS FOR TEST AND CAND DOCS ********;
*** MATCH BY SPEC, USCOREGN (USCOCOG) AND CAT - SAME FOR TEST & CONTROL;
*** ONLY EXIST FOR TEST & NEED TO PUT ON CNTL: MONTH (QTR), HELCODE, VENUE, HSAIND, USCORDT; 
DATA MASTER (KEEP=MASTERID CDOCID CIMSDRNUM TDOCID TIMSDRNUM DISTANCE MONTH QTR 
     HELCODE VENUE HSAIND USCORDT &CATVAR CAND CANDPERDOC);
SET MMF.TESTCOMP_RESULTS_AP
    MMF.TESTCOMP_RESULTS_A
    MMF.TESTCOMP_RESULTS_AM
    MMF.TESTCOMP_RESULTS_B
    MMF.TESTCOMP_RESULTS_C
    MMF.TESTCOMP_RESULTS_D;
MASTERID+1;
RUN;

DATA TESTCOMP2 (DROP=TDOCID CDOCID TIMSDRNUM CIMSDRNUM);
SET MASTER;
DOCID=TDOCID; IMSDRNUM=TIMSDRNUM; TESTCAND='T'; MTDOCID=CDOCID; MTIMSDRNUM=CIMSDRNUM; OUTPUT TESTCOMP2;
DOCID=CDOCID; IMSDRNUM=CIMSDRNUM; TESTCAND='C'; MTDOCID=TDOCID; MTIMSDRNUM=TIMSDRNUM; OUTPUT TESTCOMP2;
RUN;

PROC SORT DATA=TESTCOMP2; BY DOCID; RUN;
PROC FREQ DATA=TESTCOMP2;
     TABLES TESTCAND*CAND TESTCAND*&CATVAR HELCODE TESTCAND*VENUE TESTCAND*HSAIND / LIST MISSING;
     TITLE2 'FREQ AFTER CREATE SEPARATE ROWS FOR T/C';  RUN;

***  CHECK FOR MISSING CAT ROWS...IF SO, DETERMINE WHY...SHOULD NOT BE ***;
PROC PRINT DATA=TESTCOMP2(WHERE=(&CATVAR="  ")); RUN;

***  MERGE BACK TO GRAIL AND TIME ALIGN VARS ***************************;
***  RECORDS THAT ARE DROPPED ARE BLANK CANDIDATE RECORDS BECAUSE ******;
***       EVERY TEST DOCTOR DOES NOT HAVE A MATCHING CANDIDATE    ******;
%LET MAXPOST=%EVAL(&LATEST_HELMONTH-1); RUN;
%PUT MAXIMUM POST PERIODS IS &LATEST_HELMONTH-1 = &MAXPOST; RUN;

DATA MMF.ANALYTIC (DROP=I STOP OLDTITLE A1N: AN: DETTOT: SAMTOT: MFP1-MFP&MONUM SFP1-SFP&MONUM);
MERGE TESTCOMP2 (IN=A) MMF.SFP_INPUTSET (IN=B KEEP=DOCID USCOREGN USCOCOG SPEC OSPEC A1N: AN:
      SFP: MFP: SAMTOT: DETTOT: TARGET TALKTITLE1 RENAME=(TALKTITLE1=OLDTITLE));
BY DOCID;
IF A AND B;

IF TESTCAND='T' THEN TALKTITLE1=OLDTITLE;   /* DON'T WANT TALKTITLE ON TEST BEING USED AS CNTL */
   ELSE TALKTITLE1=' ';

ARRAY A1N(&MONUM);
ARRAY AN(&MONUM);
ARRAY MFP(&MONUM);
ARRAY SFP(&MONUM);
ARRAY DETTOT(&MONUM);
ARRAY SAMTOT(&MONUM);

ARRAY PROD_POST(&MAXPOST);
ARRAY CLASS_POST(&MAXPOST);
ARRAY MFP_POST(&MAXPOST);
ARRAY SFP_POST(&MAXPOST);
ARRAY DET_POST(&MAXPOST);
ARRAY SAM_POST(&MAXPOST);

ARRAY PROD_PRE(3);
ARRAY CLASS_PRE(3);
ARRAY MFP_PRE(3);
ARRAY SFP_PRE(3);
ARRAY DET_PRE(3);
ARRAY SAM_PRE(3);

PROD_EVENT=A1N(MONTH);
CLASS_EVENT=AN(MONTH);
MFP_EVENT=MFP(MONTH);
SFP_EVENT=SFP(MONTH);
DET_EVENT=DETTOT(MONTH);
SAM_EVENT=SAMTOT(MONTH);

STOP='N';
DO I=1 TO 3 UNTIL(STOP='Y');
   IF (MONTH+I)<=&MONUM THEN DO;
      PROD_PRE(I)=A1N(MONTH+I);
      CLASS_PRE(I)=AN(MONTH+I);
      MFP_PRE(I)=MFP(MONTH+I);
      SFP_PRE(I)=SFP(MONTH+I);
      DET_PRE(I)=DETTOT(MONTH+I);
      SAM_PRE(I)=SAMTOT(MONTH+I);
   END;
   ELSE STOP='Y';
END;

STOP='N';
DO I=1 TO (&MAXPOST) UNTIL(STOP='Y');
   IF (MONTH-I)>0 THEN DO;
      PROD_POST(I)=A1N(MONTH-I);
      CLASS_POST(I)=AN(MONTH-I);
      MFP_POST(I)=MFP(MONTH-I);
      SFP_POST(I)=SFP(MONTH-I);
      DET_POST(I)=DETTOT(MONTH-I);
      SAM_POST(I)=SAMTOT(MONTH-I);;
   END;
   ELSE STOP='Y';
END;

CLASS_PREMEAN=MEAN(OF CLASS_PRE1-CLASS_PRE3);
PROD_PREMEAN=MEAN(OF PROD_PRE1-PROD_PRE3);
DET_PREMEAN=MEAN(OF DET_PRE1-DET_PRE3);
SAM_PREMEAN=MEAN(OF SAM_PRE1-SAM_PRE3);
MFP_PREMEAN=MEAN(OF MFP_PRE1-MFP_PRE3);
SFP_PREMEAN=MEAN(OF SFP_PRE1-SFP_PRE3);

CLASS_POSTMEAN=MEAN(OF CLASS_POST1-CLASS_POST3, CLASS_EVENT);
PROD_POSTMEAN=MEAN(OF PROD_POST1-PROD_POST3, PROD_EVENT);
DET_POSTMEAN=MEAN(OF DET_POST1-DET_POST3, DET_EVENT);
SAM_POSTMEAN=MEAN(OF SAM_POST1-SAM_POST3, SAM_EVENT);
MFP_POSTMEAN=MEAN(OF MFP_POST1-MFP_POST3, MFP_EVENT);
SFP_POSTMEAN=MEAN(OF SFP_POST1-SFP_POST3);
* NOTE: SFP_POSTMEAN EXCLUDES SFP_EVENT SINCE IT IS
  ACCOUNTED FOR IN TESTFLG;

PROD_PREMEAN_LOG=LOG(PROD_PREMEAN+.5);
PROD_POSTMEAN_LOG=LOG(PROD_POSTMEAN+.5);
CLASS_PREMEAN_LOG=LOG(CLASS_PREMEAN+.5);
CLASS_POSTMEAN_LOG=LOG(CLASS_POSTMEAN+.5);

TESTFLG=(TESTCAND='T');
DISTANCE2=ROUND(DISTANCE,1.0);
USED_NORM_FULL=(N(OF &NORMFULL)=7);
USED_NORM_DTL=(N(OF &NORMDTL)=6);
USED_POI_FULL=(N(OF &POIFULL)=7);
USED_POI_DTL=(N(OF &POIDTL)=6);

*** APPEND CLUSTER USING REGION VARIABLE;
IF USCOREGN IN ('J4','J5','J7','K3','K4','K6','K8','K9','S1','S2','S5','S6','S7','S8','S9','W7','W8','W9') THEN USCOCLUS='C1';
ELSE IF USCOREGN IN ('J1','K2','W1') THEN USCOCLUS='C2';
ELSE IF USCOREGN IN ('J2','J3','J6','J8','J9','K1','K5','K7','S3','S4','W2','W3','W4','W5','W6') THEN USCOCLUS='C3';
RUN;

TITLE2 'ANALYTIC FILE';
PROC FREQ DATA=MMF.ANALYTIC;
TABLES TESTCAND HELCODE SPEC &CATVAR VENUE HSAIND USCOCOG USCOREGN USCOCLUS TARGET MONTH*QTR
       MONTH*USED_POI_FULL QTR*USED_POI_FULL / LIST MISSING; RUN;
PROC PRINT DATA=MMF.ANALYTIC (OBS=10); VAR _CHARACTER_;
PROC MEANS DATA=MMF.ANALYTIC;  RUN;


TITLE2 'ANALYSIS OF DISTANCE VARIABLE';  RUN;
PROC MEANS DATA=MMF.ANALYTIC; VAR DISTANCE; OUTPUT OUT=STD STD=STD;  RUN;
DATA STD; SET STD; STD3=STD*3;  RUN;
PROC PRINT DATA=STD; RUN;

PROC FREQ DATA=MMF.ANALYTIC; TABLES DISTANCE2/LIST MISSING;  RUN;

PROC MEANS DATA=MMF.ANALYTIC NWAY NOPRINT;
  CLASS &CATVAR USCOREGN SPEC CANDPERDOC; VAR DISTANCE;
  OUTPUT OUT=MOUT; WHERE TESTCAND='T';  RUN;

PROC TRANSPOSE DATA=MOUT OUT=TROUT PREFIX=DIST_; VAR DISTANCE; ID _STAT_;
  BY &CATVAR USCOREGN SPEC CANDPERDOC;  RUN;

DATA MMF.DISTMEANS;
LENGTH &CATVAR SPEC USCOREGN $2  CANDPERDOC $15
       CPD DIST_N DIST_MEAN DIST_MIN DIST_MAX 8.;
SET TROUT;
DROP _NAME_;
CPD=SCAN(CANDPERDOC,2,'=');
PROC PRINT DATA=MMF.DISTMEANS NOOBS;  RUN;


RUN;
PROC PRINTTO;
QUIT;


