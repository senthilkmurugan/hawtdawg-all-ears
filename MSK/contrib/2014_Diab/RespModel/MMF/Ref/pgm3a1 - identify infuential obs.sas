PROC PRINTTO LOG="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\JANTOT\PGM3a1.LOG" NEW
			PRINT="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\JANTOT\PGM3a1.LST" NEW;

*** IDENTIFY INFLUENTIAL OBS FROM OVERALL MODEL POPULATION;
*** USING: DFBETA ANALYSIS, SCATTER PLOTS OF DV * IV VARS & RESIDUAL PLOTS;
*** CHANGED FOR PROFIT PLAN 2011 TO DETERMINE CRITERIA & APPLY CRITERIA BY HELCODE;
 
TITLE1 'PGM3A - IDENTIFY INFLUENTIAL OBSERVATIONS';

OPTIONS NOCENTER MPRINT;
%LET CATVAR=CAT;          *** CURRENTLY USE CAT FOR ALL PRODUCTS (COSOPT USED CAT2);
%LET PROD=JANTOT;
%LET POIFULL= PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
%LET PATH=\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\MMF\&PROD;
*LIBNAME MMF "\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\MMF\&PROD";
LIBNAME MMF "C:\E\MMF 2012\&PROD";
RUN;
    
PROC SORT DATA=MMF.ANALYTIC;  BY HELCODE;  RUN;
DATA ANALYTIC;  SET MMF.ANALYTIC;
UNIQUE_ID+1;
RUN;

PROC GREPLAY NOFS IGOUT=WORK.GSEG;
DELETE _ALL_;
RUN;  QUIT;

*********************************************************;
*** MEANS, CORRELATIONS AND HISTOGRAMS OF MODEL VARIABLES;
*********************************************************;
FILENAME ODSOUT "&PATH";
   ODS HTML BODY="&PROD._CORR_HISTO.HTML" PATH=ODSOUT;

PROC MEANS DATA=ANALYTIC N MEAN STD MIN MAX SUM CV SKEWNESS KURTOSIS;
BY HELCODE;
VAR PROD_POSTMEAN PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
TITLE2 'MEANS OF MODEL VARIABLES';  RUN;

PROC CORR DATA=ANALYTIC;
BY HELCODE;
VAR PROD_POSTMEAN PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
TITLE2 'CORRELATON OF MODEL VARIABLES';  RUN;

DATA TEMP;  SET ANALYTIC;
RNDPROD_POSTMEAN=ROUND(PROD_POSTMEAN,1);
CHRMFP_POSTMEAN=PUT(ROUND(MFP_POSTMEAN,1),2.);
CHRSFP_POSTMEAN=PUT(ROUND(SFP_POSTMEAN,1),2.);
CHRTESTFLG=PUT(TESTFLG, 1.);
RUN;

PROC UNIVARIATE DATA=TEMP;
BY HELCODE;
VAR RNDPROD_POSTMEAN; 
HISTOGRAM / MIDPOINTS=1 TO 110 BY 5 NORMAL;
INSET MEAN STD SKEWNESS / POSITION=(75,75);
TITLE2 'HISTOGRAM OF PROD_POSTMEAN';  RUN;
QUIT;
 

****************************************;
*** PLOT CATEGORICAL PREDICTOR VARIABLES;
****************************************;
FILENAME ODSOUT "&PATH";
   ODS HTML BODY="&PROD._CATEGORVAR.HTML" PATH=ODSOUT;
   FOOTNOTE ' ';  RUN;

PROC FREQ DATA=TEMP;
TABLES HELCODE*CHRMFP_POSTMEAN HELCODE*CHRSFP_POSTMEAN HELCODE*CHRTESTFLG;
TITLE2 'FREQUENCY OF CATEGORICAL VARIABLES';  RUN;

PROC GCHART DATA=TEMP;
BY HELCODE;
VBAR CHRMFP_POSTMEAN CHRSFP_POSTMEAN CHRTESTFLG / OUTSIDE=MEAN SUMVAR=PROD_POSTMEAN TYPE=MEAN;
TITLE2 'VERTICAL BAR CHARTS OF CATEGORICAL PREDICTOR VARIABLES';
RUN;  QUIT;


***************************************;
*** PLOT CONTINUOUS PREDICTOR VARIABLES;
***************************************;
FILENAME ODSOUT "&PATH";
   ODS HTML BODY="&PROD._CONTINVAR.HTML" PATH=ODSOUT;
SYMBOL INTERPOL=NONE VALUE=DOT HEIGHT=0.1;
FOOTNOTE " ";
PROC GPLOT DATA=ANALYTIC;
BY HELCODE;
PLOT PROD_POSTMEAN*(DET_POSTMEAN SAM_POSTMEAN CLASS_PREMEAN_LOG PROD_PREMEAN_LOG);
TITLE2 'PLOTS OF DEPENDENT AND CONTINUOUS PREDICTOR VARIABLES';
RUN;  QUIT;

RUN;  QUIT;
ODS HTML CLOSE;


**********************************************************************;
*** USE OLS TO VIEW VIF IN OUTPUT QUE;
*** STATS ARE NOT AVAILABLE FOR POISSON MODEL ONLY PROC REG;
*** MIMIC POISSON MODEL AS CLOSE AS POSSIBLE USING LOG OF IV & DV VARS;
**********************************************************************;
PROC REG DATA=ANALYTIC;
MODEL PROD_POSTMEAN_LOG=&POIFULL / VIF;
BY HELCODE; 
TITLE2 'VIF ANALYSIS';
RUN; QUIT;


**********************************************************************;
*** USE OLS TO CREATE DFBETAS AND COOK'S D;
*** STATS ARE NOT AVAILABLE FOR POISSON MODEL ONLY PROC REG;
*** MIMIC POISSON MODEL AS CLOSE AS POSSIBLE USING LOG OF IV & DV VARS;
**********************************************************************;
ODS OUTPUT OUTPUTSTATISTICS=DFBETA1;
ODS LISTING CLOSE;
PROC REG DATA=ANALYTIC;
MODEL PROD_POSTMEAN_LOG=&POIFULL / INFLUENCE;
BY HELCODE;
OUTPUT OUT=OLSDATA COOKD=COOKD RESIDUAL = RESIDUAL;
RUN; QUIT;
ODS LISTING;

/*
PROC PRINT DATA=DFBETA1 (OBS=1);  TITLE2 'DFBETA DATA SET';  RUN;
PROC PRINT DATA=OLSDATA (OBS=1);  TITLE2 'OLS MODEL OUTPUT DATA SET';  RUN;

DATA OLSDATA2;
 MERGE OLSDATA DFBETA1;
 ABS_RESIDUAL=ABS(RESIDUAL);
 ABS_DFB_TESTFLG=ABS(DFB_TESTFLG);
 RUN;

ODS OUTPUT SUMMARY=NCOUNT;
PROC MEANS DATA=ANALYTIC;  BY HELCODE;  VAR PROD_PREMEAN;  
TITLE2 'NSIZE BY HELCODE';  RUN;

DATA OLSDATA3;
 MERGE OLSDATA2 NCOUNT (KEEP=PROD_PREMEAN_N HELCODE);
 BY HELCODE;
 RUN;

PROC SORT DATA=OLSDATA3 NODUPKEY; BY UNIQUE_ID; RUN;
PROC PRINT DATA=OLSDATA3 (OBS=1);  TITLE2 'OLS OUTPUT';  RUN;

PROC UNIVARIATE DATA=OLSDATA3;
CLASS HELCODE;  VAR ABS_DFB_TESTFLG;
TITLE2 'DISTRIBUTION OF ABS_DFB_TESTFLG BY HELCODE';  RUN;
*/

DATA OLSDATA;
SET OLSDATA;
BY HELCODE;
RETAIN OBSERVATION 0;
IF FIRST.HELCODE THEN OBSERVATION = 0;
OBSERVATION+1;
RUN;

PROC PRINT DATA=DFBETA1 (OBS=1);  TITLE2 'DFBETA DATA SET';  RUN;
PROC PRINT DATA=OLSDATA (OBS=1);  TITLE2 'OLS MODEL OUTPUT DATA SET';  RUN;

DATA OLSDATA2;
 MERGE OLSDATA (IN=A) DFBETA1(IN=B);
 BY HELCODE OBSERVATION RESIDUAL;
 IF A AND B;
 ABS_RESIDUAL=ABS(RESIDUAL);
 ABS_DFB_TESTFLG=ABS(DFB_TESTFLG);
 RUN;

ODS OUTPUT SUMMARY=NCOUNT;
PROC MEANS DATA=ANALYTIC;  BY HELCODE;  VAR PROD_PREMEAN;  
TITLE2 'NSIZE BY HELCODE';  RUN;

DATA OLSDATA3;
 MERGE OLSDATA2 NCOUNT (KEEP=PROD_PREMEAN_N HELCODE);
 BY HELCODE;
 RUN;

PROC SORT DATA=OLSDATA3 NODUPKEY; BY UNIQUE_ID; RUN;
PROC PRINT DATA=OLSDATA3 (OBS=1);  TITLE2 'OLS OUTPUT';  RUN;

PROC UNIVARIATE DATA=OLSDATA3;
CLASS HELCODE;  VAR ABS_DFB_TESTFLG;
TITLE2 'DISTRIBUTION OF ABS_DFB_TESTFLG BY HELCODE';  RUN;

***********************************;
*** USE POISSON TO CREATE RESIDUALS;
***********************************;

ODS LISTING CLOSE;
PROC GENMOD DATA=ANALYTIC;
MODEL PROD_POSTMEAN=&POIFULL / DIST=POI LINK=LOG PSCALE RESIDUALS OBSTATS;
BY HELCODE;
OUTPUT OUT=POIDATA STDRESCHI=POIRESID P=PREDICTED;
RUN; QUIT;
ODS LISTING;

PROC SORT DATA=POIDATA NODUPKEY;  BY UNIQUE_ID;  RUN;
PROC PRINT DATA=POIDATA (OBS=1);  TITLE2 'POISSON OUTPUT';  RUN;

DATA MMF.RESULTS;
 MERGE POIDATA OLSDATA3;
 BY UNIQUE_ID; 
 ABS_POIRESID=ABS(POIRESID);
 RUN;

FILENAME ODSOUT "&PATH";
   ODS HTML BODY="&PROD._PRE_INFLDIAGNSTC.HTML" PATH=ODSOUT;
SYMBOL INTERPOL=NONE VALUE=DOT HEIGHT=0.1;
FOOTNOTE " ";
PROC GPLOT DATA=MMF.RESULTS;
BY HELCODE;
PLOT ABS_POIRESID * PREDICTED;
PLOT COOKD * UNIQUE_ID;
PLOT ABS_DFB_TESTFLG * UNIQUE_ID;
TITLE2 'DIAGNOSTIC PLOTS PRIOR TO INFLUENTIAL OBSERVATION REMOVAL';
RUN;  QUIT;

RUN;  QUIT;
ODS HTML CLOSE;

*********************************************************;
***Place cutoffs into excel with the following variables
*********************************************************;
proc freq data=Analytic;
table Helcode/out=List_Hel(keep=Helcode);
run;

data list_hel;
length product $20. helcode $20. valres 8. valcook 8. valdfb 8.;
	set list_hel;
	product="&prod.";
	VALRES=.;
	VALCOOK=.;
	VALDFB=.;
run;

PROC EXPORT DATA=lIST_HEL Outfile="&PATH\INFL OBS CUTOFFS &PROD."
dbms=excel replace;
run;

PROC PRINTTO;
QUIT;


