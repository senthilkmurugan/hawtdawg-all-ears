PROC PRINTTO LOG="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\pgm3b1 - run model all.LOG" NEW
			PRINT="\\WPUSHH01\DINFOPLN\Marketing Mix PI\MMF 2012\MMF\ZVTTOT\pgm3b1 - run model all.LST" NEW;

* PRODUCT NRX MODEL;
* 4-28-11 ADDED TVALUE TO NORM DATASET AND CHISQ VALUE TO POI DATASET; 
* 2-6-12  ADDED CHECK FOR NEGATIVE IMPACT;
TITLE " ";
TITLE2 'PGM3B - MMF MODELING - PRODUCT NRX MODEL';

OPTIONS NOCENTER MPRINT;
*LIBNAME MMF '\\WPUSHH01\DINFOPLN\MARKETING MIX PI\MMF 2012\MMF\ZVTTOT';
LIBNAME MMF 'C:\E\MMF 2012\ZVTTOT';  RUN;
%LET CATVAR=CAT;            *** CURRENTLY USE CAT FOR ALL PRODUCTS EXCEPT COSOPT;
RUN;

*** MACRO VARS FOR INDEPENDENT MODEL VARS;
*** FULL MODEL IS THE TRADITIONAL MODEL FOR ROI CALCULATION;
*** DTL MODEL MEASURES IMPACT OF ADDITIONAL DETAILS AS RESULT OF EVENT;
%LET NORMFULL=PROD_PREMEAN CLASS_PREMEAN SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
%LET NORMDTL= PROD_PREMEAN CLASS_PREMEAN SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN;
%LET POIFULL= PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN TESTFLG;
%LET POIDTL=  PROD_PREMEAN_LOG CLASS_PREMEAN_LOG SAM_POSTMEAN DET_POSTMEAN SFP_POSTMEAN MFP_POSTMEAN;
RUN;

PROC DATASETS LIBRARY = WORK NOLIST KILL;
RUN;QUIT;

*PROC CONTENTS DATA=MMF.MODEL; *RUN;

*** MACRO VARS FOR MODEL STRATIFICATIONS;
%LET MODNAME1=FULL;             %LET MODNAME2=DTL;
%LET CRITER1=' ';               %LET CRITER2='C';
%LET BY1= ;                     %LET NAME1=COMBINED;
%LET BY2=BY &CATVAR;            %LET NAME2=CR;
%LET BY3=BY HELCODE;            %LET NAME3=HEL;
%LET BY4=BY HELCODE &CATVAR;    %LET NAME4=HELCR;
%LET BY5=BY SPEC;               %LET NAME5=SPEC;
%LET BY6=BY HELCODE SPEC;       %LET NAME6=HELSPEC;
%LET BY7=BY USCOCOG;            %LET NAME7=COG;
%LET BY8=BY HELCODE USCOCOG;    %LET NAME8=HELCOG;
%LET BY9=BY VENUE;              %LET NAME9=VENUE;
%LET BY10=BY HELCODE VENUE;     %LET NAME10=HELVENUE;
%LET BY11=BY USCOCLUS;          %LET NAME11=CLUSTER;
%LET BY12=BY HELCODE USCOCLUS;  %LET NAME12=HELCLUS;
RUN;


*** MODELS;
%MACRO MOD;
%DO J=1 %TO 12; * BY-GROUP ITERATIONS;
%DO I=1 %TO 2; * FULL AND DETAIL MODELS;

* SORT BY BY-GROUP (EXCEPT FIRST/COMBINED ITERATION);

%IF &J NE 1 %THEN %DO;
  PROC SORT DATA=MMF.MODEL;
  &&BY&J; RUN;
%END;

* NORM OLS REG MODEL;
ODS OUTPUT PARAMETERESTIMATES=NORM&I.&J;
PROC REG DATA=MMF.MODEL;
WHERE TESTCAND NE &&CRITER&I;
NORM_&&NAME&J: MODEL PROD_POSTMEAN=&&&&NORM&&MODNAME&I;
&&BY&J;
RUN; QUIT;

* POI MODEL: LINK=LOG FUNCTION TAKES LOG OF DEPENDENT VAR PROD_POSTMEAN;
ODS OUTPUT PARAMETERESTIMATES=POI&I.&J;
PROC GENMOD DATA=MMF.MODEL;
WHERE TESTCAND NE &&CRITER&I;
MODEL PROD_POSTMEAN=&&&&POI&&MODNAME&I / DIST=POI LINK=LOG PSCALE ALPHA=0.25;
&&BY&J;
RUN; QUIT;

%END; %END;
%MEND;
%MOD;


*** DETERMINE OBSERVATIONS PER MODEL (HAVE ALL MODEL VARS);
%LET CLASS1= ;
%LET CLASS2=CLASS  &CATVAR;
%LET CLASS3=CLASS  HELCODE;
%LET CLASS4=CLASS  HELCODE &CATVAR;
%LET CLASS5=CLASS  SPEC;
%LET CLASS6=CLASS  HELCODE SPEC;
%LET CLASS7=CLASS  USCOCOG;
%LET CLASS8=CLASS  HELCODE USCOCOG;
%LET CLASS9=CLASS  VENUE;
%LET CLASS10=CLASS HELCODE VENUE;
%LET CLASS11=CLASS USCOCLUS;
%LET CLASS12=CLASS HELCODE USCOCLUS;

RUN;

%MACRO OBSUSED;
%DO J=1 %TO 12; * BY-GROUP ITERATIONS;
%DO I=1 %TO 2; * FULL AND DETAIL MODELS;
PROC MEANS NOPRINT DATA=MMF.MODEL NWAY;
  OUTPUT OUT=NORMCOUNTS&I.&J N=&&MODNAME&I.._OBSUSED;
  &&CLASS&J; VAR PROD_POSTMEAN;
  WHERE USED_NORM_&&MODNAME&I AND TESTCAND NE &&CRITER&I;
PROC MEANS NOPRINT DATA=MMF.MODEL NWAY;
  OUTPUT OUT=POICOUNTS&I.&J N=&&MODNAME&I.._OBSUSED;
  &&CLASS&J; VAR PROD_POSTMEAN;
  WHERE USED_POI_&&MODNAME&I AND TESTCAND NE &&CRITER&I;;
RUN;
%END; %END;
%MEND;
%OBSUSED;


*** CALC CAND DOCS MEAN PROD_POSTMEAN;
%MACRO POSTMEAN;
%DO J=1 %TO 12; * BY-GROUP ITERATIONS;
PROC MEANS NOPRINT DATA=MMF.MODEL NWAY;
OUTPUT OUT=POSTMEAN&J MEAN=;
&&CLASS&J; VAR PROD_POSTMEAN; WHERE TESTCAND='C';
RUN;
%END; %MEND;
%POSTMEAN;


*** COMBINE OUTPUT FILES OF NORMAL REG - CREATE RECORDS FOR MISSING CHANNELS;  
DATA MISSVALUE;
   LENGTH TYPE $8 HELCODE $10 &CATVAR $6 SPEC $4 USCOCOG $3 VENUE $10 USCOCLUS $3;
   INPUT TYPE HELCODE $ &CATVAR $ SPEC $ USCOCOG $ VENUE $ USCOCLUS $;
   CARDS;
   HELCOG PFI . . NC . .
   HELCOG PFI . . NE . .
   HELCOG PFI . . SE . .
   HELCOG PFI . . WE . .
   HEL PFI . . . . .
   HELCR PFI A+ . . . .
   HELCR PFI A . . . .
   HELCR PFI A- . . . .
   HELCR PFI B  . . . .
   HELCR PFI C  . . . .
   HELCR PFI D  . . . .
   HELSPEC PFI . SP . . .
   HELSPEC PFI . PC . . .
   HELVENUE PFI . . . IN .
   HELVENUE PFI . . . OUT .
   HELCLUS PFI . . . . C1
   HELCLUS PFI . . . . C2
   HELCLUS PFI . . . . C3
   HELCOG SYMPOSIUM . . NC . .
   HELCOG SYMPOSIUM . . NE . .
   HELCOG SYMPOSIUM . . SE . .
   HELCOG SYMPOSIUM . . WE . .
   HEL SYMPOSIUM . . . . .
   HELCR SYMPOSIUM A+ . . . .
   HELCR SYMPOSIUM A . . . .
   HELCR SYMPOSIUM A- . . . .
   HELCR SYMPOSIUM B  . . . .
   HELCR SYMPOSIUM C  . . . .
   HELCR SYMPOSIUM D  . . . .
   HELSPEC SYMPOSIUM . SP . . .
   HELSPEC SYMPOSIUM . PC . . .
   HELVENUE SYMPOSIUM . . . OUT .
   HELCLUS SYMPOSIUM . . . . C1
   HELCLUS SYMPOSIUM . . . . C2
   HELCLUS SYMPOSIUM . . . . C3
   ;
   RUN;

DATA NORMVALUE;
  SET MISSVALUE;
  LENGTH MODEL $15;
  MODEL='NORM_'||TYPE;
  RUN;

PROC PRINT DATA=NORMVALUE;  RUN;

DATA NORMFULL;
  SET NORM11 NORM12 NORM13 NORM14 NORM15 NORM16 NORM17 NORM18 NORM19 NORM110 NORM111 NORM112;
  IF VARIABLE='TESTFLG';
  RENAME ESTIMATE=FULL_ESTIMATE PROBT=FULL_PROBT TVALUE=FULL_TVALUE;
DATA NORMDTL;
  SET NORM21 NORM22 NORM23 NORM24 NORM25 NORM26 NORM27 NORM28 NORM29 NORM210 NORM211 NORM212;
  IF VARIABLE='DET_POSTMEAN';
  RENAME ESTIMATE=DTL_ESTIMATE PROBT=DTL_PROBT;
DATA NORMFULLCOUNT;
  SET NORMCOUNTS11 NORMCOUNTS12 NORMCOUNTS13 NORMCOUNTS14 NORMCOUNTS15 NORMCOUNTS16
      NORMCOUNTS17 NORMCOUNTS18 NORMCOUNTS19 NORMCOUNTS110 NORMCOUNTS111 NORMCOUNTS112;
DATA NORMDTLCOUNT;
  SET NORMCOUNTS21 NORMCOUNTS22 NORMCOUNTS23 NORMCOUNTS24 NORMCOUNTS25 NORMCOUNTS26
      NORMCOUNTS27 NORMCOUNTS28 NORMCOUNTS29 NORMCOUNTS210 NORMCOUNTS211 NORMCOUNTS212;
RUN;

PROC SORT DATA=NORMVALUE;  BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;  
PROC SORT DATA=NORMFULL; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=NORMDTL; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=NORMFULLCOUNT; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=NORMDTLCOUNT; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
RUN;

DATA MMF.NORM (KEEP=FULL_ESTIMATE FULL_PROBT FULL_TVALUE DTL_ESTIMATE DTL_PROBT
     MODEL LEVEL FULL_OBSUSED DTL_OBSUSED &CATVAR HELCODE SPEC USCOCOG USCOCLUS VENUE);
LENGTH MODEL LEVEL $15 HELCODE $10 &CATVAR $6 SPEC $4 USCOCOG $3 VENUE $10 USCOCLUS $3;
MERGE NORMVALUE NORMFULL NORMDTL NORMFULLCOUNT NORMDTLCOUNT;
BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
IF &CATVAR=' ' THEN &CATVAR=' ALL';
   ELSE IF &CATVAR='A+' THEN &CATVAR='A1';
   ELSE IF &CATVAR='A'  THEN &CATVAR='A2';
   ELSE IF &CATVAR='A-' THEN &CATVAR='A3';
IF HELCODE=' ' THEN HELCODE='ALL';
IF SPEC=' ' THEN SPEC='ALL';
IF USCOCOG=' ' THEN USCOCOG='ALL';
IF VENUE=' ' THEN VENUE='ALL';
IF USCOCLUS=' ' THEN USCOCLUS='ALL';
IF MODEL='NORM_HELCOG' THEN LEVEL=TRIM(HELCODE)||'_'||USCOCOG;
   ELSE IF MODEL='NORM_HELCLUS' THEN LEVEL=TRIM(HELCODE)||'_'||USCOCLUS;
   ELSE IF MODEL='NORM_HELVENUE' THEN LEVEL=TRIM(HELCODE)||'_'||VENUE;
   ELSE IF MODEL IN ('NORM_HEL','NORM_HELCR','NORM_HELSPEC') THEN LEVEL=TRIM(HELCODE)||'_ALL';
RUN;

PROC SORT DATA=MMF.NORM; BY HELCODE SPEC &CATVAR USCOCOG VENUE USCOCLUS;
TITLE3 'NORMAL REG MODEL RESULTS';
PROC PRINT DATA=MMF.NORM;
RUN;

*** COMBINE OUTPUT FILES OF POI REG;
DATA POIVALUE;
  SET MISSVALUE;
  LENGTH MODEL $15;
  MODEL='POI_'||TYPE;
DATA POIFULL;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POI11 POI12 POI13 POI14 POI15 POI16 POI17 POI18 POI19 POI110 POI111 POI112;
  IF PARAMETER='TESTFLG';
  RENAME ESTIMATE=FULL_ESTIMATE PROBCHISQ=FULL_PROBCHISQ CHISQ=FULL_CHISQ LOWERWALDCL=FULL_LOWERWALDCL UPPERWALDCL=FULL_UPPERWALDCL;
DATA POIFULLDTL;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POI11 POI12 POI13 POI14 POI15 POI16 POI17 POI18 POI19 POI110 POI111 POI112;
  IF PARAMETER='DET_POSTMEAN';
  RENAME ESTIMATE=FULLDTL_ESTIMATE PROBCHISQ=FULLDTL_PROBCHISQ;
DATA POIDTL;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POI21 POI22 POI23 POI24 POI25 POI26 POI27 POI28 POI29 POI210 POI211 POI212;
  IF PARAMETER='DET_POSTMEAN';
  RENAME ESTIMATE=DTL_ESTIMATE PROBCHISQ=DTL_PROBCHISQ;
DATA POIFULLCOUNT;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POICOUNTS11 POICOUNTS12 POICOUNTS13 POICOUNTS14 POICOUNTS15 POICOUNTS16
      POICOUNTS17 POICOUNTS18 POICOUNTS19 POICOUNTS110 POICOUNTS111 POICOUNTS112;
DATA POIDTLCOUNT;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POICOUNTS21 POICOUNTS22 POICOUNTS23 POICOUNTS24 POICOUNTS25 POICOUNTS26
      POICOUNTS27 POICOUNTS28 POICOUNTS29 POICOUNTS210 POICOUNTS211 POICOUNTS212;
DATA POSTMEANS;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET POSTMEAN1 POSTMEAN2 POSTMEAN3 POSTMEAN4 POSTMEAN5 POSTMEAN6 POSTMEAN7 POSTMEAN8 POSTMEAN9 POSTMEAN10 POSTMEAN11 POSTMEAN12;
DATA MODNAMES;
	LENGTH HELCODE $ 10.;
	INFORMAT HELCODE $ 10.;
	FORMAT HELCODE $ 10.;
  SET NORMFULL (KEEP=MODEL &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS);
  MODEL='POI'||SUBSTR(MODEL,5);
RUN;

PROC SORT DATA=POIVALUE; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POIFULL; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POIFULLDTL; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POIDTL; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POIFULLCOUNT; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POIDTLCOUNT; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=POSTMEANS; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
PROC SORT DATA=MODNAMES; BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
RUN;

DATA MMF.POI (KEEP=FULL_ESTIMATE FULL_ESTADJ FULL_PROBCHISQ FULL_CHISQ FULL_LOWERWALDCL FULL_UPPERWALDCL
     FULL_LOWADJ FULL_UPPADJ FULLDTL_ESTIMATE FULLDTL_ESTADJ FULLDTL_PROBCHISQ DTL_ESTIMATE
     DTL_ESTADJ DTL_PROBCHISQ MODEL LEVEL FULL_OBSUSED DTL_OBSUSED
     PROD_POSTMEAN &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS);
LENGTH MODEL LEVEL $15 HELCODE $10 &CATVAR $6 SPEC $4 USCOCOG $3 VENUE $10 USCOCLUS $3;
MERGE POIVALUE POIFULL POIFULLDTL POIDTL POIFULLCOUNT POIDTLCOUNT POSTMEANS MODNAMES;
BY &CATVAR HELCODE SPEC USCOCOG VENUE USCOCLUS;
IF &CATVAR=' ' THEN &CATVAR=' ALL';
   ELSE IF &CATVAR='A+' THEN &CATVAR='A1';
   ELSE IF &CATVAR='A'  THEN &CATVAR='A2';
   ELSE IF &CATVAR='A-' THEN &CATVAR='A3';
IF HELCODE=' ' THEN HELCODE='ALL';
IF SPEC=' ' THEN SPEC='ALL';
IF USCOCOG=' ' THEN USCOCOG='ALL';
IF VENUE=' ' THEN VENUE='ALL';
IF USCOCLUS=' ' THEN USCOCLUS='ALL';
IF MODEL='POI_HELCOG' THEN LEVEL=TRIM(HELCODE)||'_'||USCOCOG;
   ELSE IF MODEL='POI_HELCLUS' THEN LEVEL=TRIM(HELCODE)||'_'||USCOCLUS;
   ELSE IF MODEL='POI_HELVENUE' THEN LEVEL=TRIM(HELCODE)||'_'||VENUE;
   ELSE IF MODEL IN ('POI_HEL','POI_HELCR','POI_HELSPEC') THEN LEVEL=TRIM(HELCODE)||'_ALL';

*CONVERT RESPONSE ESTIMATES AND CONFIDENCE INTERVALS BACK TO NRXS;

TEMPVAR=EXP(FULL_ESTIMATE)*PROD_POSTMEAN;
FULL_ESTADJ=TEMPVAR-PROD_POSTMEAN;
FULLDTL_ESTADJ=(EXP(FULLDTL_ESTIMATE)*TEMPVAR)-TEMPVAR;
DTL_ESTADJ=(EXP(DTL_ESTIMATE)*TEMPVAR)-TEMPVAR;

FULL_LOWADJ=(EXP(FULL_LOWERWALDCL)*PROD_POSTMEAN)-PROD_POSTMEAN;
FULL_UPPADJ=(EXP(FULL_UPPERWALDCL)*PROD_POSTMEAN)-PROD_POSTMEAN;
RUN;

PROC SORT DATA=MMF.POI; BY HELCODE SPEC &CATVAR USCOCOG VENUE USCOCLUS;
TITLE3 'POISSON MODEL RESULTS';
PROC PRINT DATA=MMF.POI; RUN;
PROC CONTENTS DATA=MMF.POI; RUN;

PROC PRINT DATA=MMF.POI;
WHERE MODEL='POI_HEL'; RUN;

DATA MMF.NEGATIVE_SIGNIF_ALL; 
SET MMF.POI;
WHERE MODEL='POI_HEL' AND FULL_ESTADJ<0 AND FULL_PROBCHISQ > 0.25; 
TITLE3 'Check Negative Impacts for Significance - If negative impact and significant, then FLAG FOR FURTHER REVIEW!!!!!!'; 
RUN;

** FINANCIAL PROGRAM SETS NEGATIVE ESTIMATES TO ZERO & ADJUSTS THEIR CONFIDENCE INTERVAL - MAKE SURE NOT NEGATIVE & SIGNIFICANT;

PROC PRINTTO;
QUIT;
