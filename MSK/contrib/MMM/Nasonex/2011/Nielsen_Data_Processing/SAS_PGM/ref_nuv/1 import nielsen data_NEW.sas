PROC PRINTTO LOG="\\wpushh01\dinfopln\PRA\DTC\NuvaRing\2011 profit plan\SAS Logs and Lists\1 import nielsen data.LOG" NEW
			PRINT="\\wpushh01\dinfopln\PRA\DTC\NuvaRing\2011 profit plan\SAS Logs and Lists\1 import nielsen data.LST" NEW;
***************************************************************************;
* IMPORT NATIONAL AND DMA-LEVEL DATA RECEIVED FROM NMR -                   ;
* TIME PERIOD IS 2007 THROUGH 2009                                         ;
*                                                                 ;
***************************************************************************;
TITLE "NUVARING NMR DATA IMPORT - JAN 2010 THROUGH JUN 2010";
*LIBNAME PDTC "\\WPUSHH01\DINFOPLN\PRA\DTC\NuvaRing\2011 profit plan\SAS datasets";
LIBNAME DTC "\\WPUSHH01\DINFOPLN\PRA\DTC\NuvaRing\2011 profit plan\SAS datasets";
* DELETE WORKFILES IN CASE OF RE-RUN;
PROC DATASETS LIB=WORK NOLIST; 
  DELETE 
  NATLTV_GRPS 
  /*NATLTV_SPEND 
  NATL_MM */
  LOCLTV_GRPS 
  /*LOCLTV_SPEND 
  LOCL_MM */
  NATLTV 
  NATL 
  LOCL
; RUN; QUIT;

* IMPORT NATL AND LOCL TV AND MULTIMEDIA DATA (GRPS, MULTIMEDIA, SPEND);
OPTIONS MPRINT MLOGIC SYMBOLGEN;
%MACRO NGETDATA(FILENAME,SHEETNAME,FCELL,LCELL,OUTNAME);
  PROC IMPORT OUT= WORK.&OUTNAME (WHERE=(REPORT_PERIOD~=.))
  DATAFILE= "\\WPUSHH01\DINFOPLN\PRA\DTC\Nielsen data\Nuvaring\&FILENAME..XLS"
  DBMS=EXCEL REPLACE; SHEET="&SHEETNAME"; GETNAMES=YES; RANGE="&FCELL:&LCELL"; MIXED=YES;
  SCANTEXT=YES; USEDATE=YES; SCANTIME=YES; RUN;
%MEND;

* NATL TV - GRPS;
%NGETDATA(NuvaRing National TV GRPs Jan - June 2010,Jan - June 2010,A9,P540,NATLTV_GRPS);

* NATL TV - SPEND;
%NGETDATA(NuvaRing National TV $$$ Jan - June 2010,Jan - June 2010,A9,K540,NATLTV_SPEND);

* NATL MULTIMEDIA;
%NGETDATA(NuvaRing National Multi Jan - June 2010,Jan - June 2010,A6,M90,NATL_MM);


* LOCL TV - GRPS;
%NGETDATA(NuvaRing Local TV GRPs Jan - June 2010,Jan - June 2010,A9,L6781,LOCLTV_GRPS);

* LOCL TV - SPEND;
%NGETDATA(NuvaRing Local TV $$$ Jan - June 2010,Jan - June 2010,A9,G156,LOCLTV_SPEND);

* LOCL MULTIMEDIA;
%NGETDATA(NuvaRing Local Multi Jan - June 2010,Jan - June 2010,A6,N8,LOCL_MM);


PROC SORT DATA=NATLTV_GRPS;  BY MARKET BRAND REPORT_PERIOD TV_DAYPART 
							    COMMERCIAL_DURATION CREATIVE; RUN;
PROC SORT DATA=NATLTV_SPEND; BY MARKET BRAND REPORT_PERIOD TV_DAYPART 
							    COMMERCIAL_DURATION CREATIVE; RUN;
DATA NATLTV; MERGE NATLTV_GRPS NATLTV_SPEND;
	BY MARKET BRAND REPORT_PERIOD TV_DAYPART COMMERCIAL_DURATION CREATIVE; RUN;

* MERGE NATL TV AND MULTIMEDIA DATA;
PROC SORT DATA=NATLTV;  BY MARKET REPORT_PERIOD BRAND; RUN;

PROC SORT DATA=NATL_MM; BY MARKET REPORT_PERIOD BRAND; RUN;

PROC CONTENTS DATA=NATLTV; RUN;
PROC CONTENTS DATA=NATL_MM; RUN;

* CHANGE VARIABLE NAMES FOR NATL TV ;
DATA NATL (RENAME=(
	 Cable_TV__GRP__F18_49_=CABTV_F1849
     CABLE_TV__GRP__HHLDS_=CABTV_HH
     NETWORK_TV__GRP__HHLDS_=NETTV_HH
     Network_TV__GRP__F18_49_=NETTV_F1849
     SYNDICATED_TV__GRP__HHLDS_=SYNTV_HH
     Syndicated_TV__GRP__F18_49_=SYNTV_F1849
     Spanish_Language_Cable_TV__GRP_0=CABTV_F1849_SPANISH
     Spanish_Language_Cable_TV__GRP__=CABTV_HH_SPANISH
     Spanish_Language_Network_TV__GR0=NETTV_F1849_SPANISH
     Spanish_Language_Network_TV__GRP=NETTV_HH_SPANISH
	 CABLE_TV_______000__=CABTV_SPEND
     NETWORK_TV_______000__=NETTV_SPEND
     SYNDICATED_TV_______000__=SYNTV_SPEND
     INTERNET__UNITS_=INET_UNITS
     INTERNET_______000__=INET_SPEND
     NATIONAL_MAGAZINE__UNITS_=MAG_UNITS
     NATIONAL_MAGAZINE_______000__=MAG_SPEND
     NATIONAL_NEWSPAPER__UNITS_=NEWS_UNITS
     NATIONAL_NEWSPAPER_______000__=NEWS_SPEND
     NATIONAL_SUNDAY_SUPPLEMENT__UNIT=SUP_UNITS
     NATIONAL_SUNDAY_SUPPLEMENT______=SUP_SPEND
	 NETWORK_RADIO__UNITS_=RAD_UNITS
     NETWORK_RADIO_______000__=RAD_SPEND
     TV_DAYPART=DAYPART));
SET NATLTV (RENAME=(BRAND=LONG_BRAND) IN=T) NATL_MM (RENAME=(BRAND=LONG_BRAND)); 
BY MARKET REPORT_PERIOD LONG_BRAND; BRAND=UPCASE(SCAN(LONG_BRAND,1)); 
IF T THEN RECTYPE="TV"; ELSE RECTYPE="MM"; 
IF RECTYPE="MM" THEN DO; TV_DAYPART="NA"; COMMERCIAL_DURATION="NA";CREATIVE="NA"; END; RUN;

TITLE2 "LIST CONTENTS OF NATIONAL TV "; RUN;
PROC CONTENTS DATA=NATL VARNUM; RUN;
TITLE2 ""; RUN;

PROC SORT DATA=LOCLTV_GRPS; 
	BY MARKET BRAND REPORT_PERIOD TV_DAYPART COMMERCIAL_DURATION CREATIVE; RUN;

PROC SORT DATA=LOCLTV_SPEND; 
	BY MARKET BRAND REPORT_PERIOD TV_DAYPART COMMERCIAL_DURATION CREATIVE; RUN;


* MERGE LOCL TV GRP/IMP AND SPEND DATA;
DATA LOCLTV; MERGE LOCLTV_GRPS LOCLTV_SPEND; 
	BY MARKET BRAND REPORT_PERIOD TV_DAYPART COMMERCIAL_DURATION CREATIVE; RUN;
PROC SORT DATA=LOCLTV; BY MARKET BRAND REPORT_PERIOD; RUN;
PROC SORT DATA=LOCL_MM; BY MARKET BRAND REPORT_PERIOD; RUN;

* MERGE LOCL TV GRP DATA;
* CHANGE VARIABLE NAMES FOR LOCL TV;
DATA LOCL  (RENAME=(
            NETWORK_CLEARANCE_SPOT_TV__GRP__=NETSPOTTV_HH
            NETWORK_CLEARANCE_SPOT_TV__GRP_0=NETSPOTTV_F1849
            SPOT_TV__GRP__HHLDS_=SPOTTV_HH
            SPOT_TV__GRP__F18_49_=SPOTTV_F1849
            SYNDICATED_CLEARANCE_SPOT_TV__G0=SYNSPOTTV_F1849
            SYNDICATED_CLEARANCE_SPOT_TV__GR=SYNSPOTTV_HH
			SPOT_TV_______000__=SPOTTV_SPEND
            FSI_COUPON__UNITS_=FSI_UNITS
            FSI_COUPON_______000__=FSI_SPEND
            LOCAL_MAGAZINE__UNITS_=LMAG_UNITS
            LOCAL_MAGAZINE_______000__=LMAG_SPEND
            LOCAL_NEWSPAPER__UNITS_=LNEWS_UNITS
            LOCAL_NEWSPAPER_______000__=LNEWS_SPEND
            LOCAL_SUNDAY_SUPPLEMENT__UNITS_=LSUNSUP_UNITS
            LOCAL_SUNDAY_SUPPLEMENT_______00=LSUNSUP_SPEND
            OUTDOOR_______000__=OUT_SPEND
            SPOT_RADIO__UNITS_=SPOTRADIO_UNITS
            SPOT_RADIO_______000__=SPOTRADIO_SPEND
            TV_DAYPART=DAYPART));
SET LOCLTV (RENAME=(BRAND=LONG_BRAND) IN=T) LOCL_MM (RENAME=(BRAND=LONG_BRAND)); 
BY MARKET LONG_BRAND REPORT_PERIOD; BRAND=UPCASE(SCAN(LONG_BRAND,1));
IF T THEN RECTYPE="TV"; ELSE RECTYPE="MM"; 
IF RECTYPE="MM" THEN DO; TV_DAYPART="NA"; COMMERCIAL_DURATION="NA"; CREATIVE="NA"; END;RUN;

PROC SORT DATA=LOCL; BY MARKET BRAND REPORT_PERIOD; RUN;

 
PROC FREQ DATA=LOCL; TABLES RECTYPE/LIST MISSING; RUN;


TITLE2 "LIST CONTENTS OF LOCAL TV AND MULTIMEDIA DATASET"; RUN;
PROC CONTENTS DATA=LOCL VARNUM; RUN;
TITLE2 ""; RUN;

PROC FREQ DATA=NATL; TABLES COMMERCIAL_DURATION/LIST MISSING; RUN;
PROC FREQ DATA=LOCL; TABLES COMMERCIAL_DURATION/LIST MISSING; RUN;

* MAKE NATL AND LOCL DATASETS PERMANENT;
DATA DTC.NATL_TV_MM_JAN10_JUN10; SET NATL; YEAR=YEAR(REPORT_PERIOD); MONTH=MONTH(REPORT_PERIOD);
											WEEK=WEEK(REPORT_PERIOD);DAY=DAY(REPORT_PERIOD);
ARRAY NUMS(*) _NUMERIC_; DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; END; 
TEMP=SUBSTR((STRIP(COMMERCIAL_DURATION)),1,2);
IF TEMP='NA' THEN TEMP=" ";
DURATION=INPUT(TEMP,10.); RUN;

PROC FREQ DATA=DTC.NATL_TV_MM_JAN10_JUN10; TABLES COMMERCIAL_DURATION*DURATION/LIST MISSING; RUN; 

DATA DTC.LOCL_TV_MM_JAN10_JUN10; SET LOCL; YEAR=YEAR(REPORT_PERIOD); MONTH=MONTH(REPORT_PERIOD);
												WEEK=WEEK(REPORT_PERIOD);DAY=DAY(REPORT_PERIOD); 
ARRAY NUMS(*) _NUMERIC_; DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; END; 
TEMP=SUBSTR((STRIP(COMMERCIAL_DURATION)),1,2);
IF TEMP='NA' THEN TEMP=" ";
DURATION=INPUT(TEMP,10.);
RUN;
 PROC FREQ DATA=DTC.LOCL_TV_MM_JAN10_JUN10; TABLES COMMERCIAL_DURATION*DURATION/LIST MISSING; RUN; 

PROC FREQ DATA=DTC.NATL_TV_MM_JAN10_JUN10; TABLES BRAND*CREATIVE*YEAR*MONTH/LIST MISSING ; RUN;
PROC FREQ DATA=DTC.LOCL_TV_MM_JAN10_JUN10; TABLES BRAND*CREATIVE*YEAR*MONTH/OUT=FREQCNT LIST MISSING; RUN;
PROC FREQ DATA=DTC.NATL_TV_MM_JAN10_JUN10; TABLES DURATION/LIST MISSING ; RUN;
PROC FREQ DATA=DTC.LOCL_TV_MM_JAN10_JUN10; TABLES DURATION/LIST MISSING; RUN;

DATA A;
   SET DTC.NATL_TV_MM_JAN10_JUN10;
RUN;
PROC SORT DATA=A; BY BRAND;RUN;

PROC FREQ DATA=A; TABLES BRAND; RUN;

PROC MEANS DATA= A; VAR CABTV_F1849_SPANISH  NETTV_F1849_SPANISH; BY BRAND; RUN;


PROC PRINTTO;
QUIT;
