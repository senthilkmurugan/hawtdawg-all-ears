***************************************************************************;
* ROLLUP WEEKLY BRAND-CREATIVE-DAYPART-MARKET DATA TO MONTHLY FOR DATA     ;
* DESCRIPTION/GRAPHING AND MODELING DATA PREP...                           ;
***************************************************************************;
TITLE "NMR DATA ROLLUP TO DMA-MONTHLY - JAN 2007 THROUGH DEC 2009";
OPTIONS PAGESIZE=MAX;

* LIBRARY STATEMENTS FOR INPUT DATASETS;
LIBNAME TEMP "\\WPUSHH01\DINFOPLN\PRA\DTC\NuvaRing\2010 profit plan\SAS datasets"; 
/*LIBNAME TEMP "C:\PROJECTS\DTC\SAS DATASETS\SINGULAIR";*SOME DATA ARE ON C DRIVE TO RUN FASTER"; */
* LIBRARY AND PATH FOR OUTPUT FILES;
LIBNAME NEWOUT "\\WPUSHH01\DINFOPLN\PRA\DTC\NuvaRing\2010 profit plan\SAS datasets";

* CREATE DATASET OF ALL POSSIBLE YEAR-MONTHS IN RANGE;
DATA YEARMONTHS;
INPUT YEAR MONTH @@;
MONTHLABEL=PUT((MDY(MONTH,1,YEAR)),MONYY5.);
QTRLABEL=PUT((MDY(MONTH,1,YEAR)),YYQ4.);
CARDS;
2007 1 2007 2 2007 3 2007 4 2007 5 2007 6 2007 7 2007 8 2007 9 2007 10 2007 11 2007 12 
2008 1 2008 2 2008 3 2008 4 2008 5 2008 6 2008 7 2008 8 2008 9 2008 10 2008 11 2008 12
2009 1 2009 2 2009 3 2009 4 2009 5 2009 6 2009 7 2009 8 2009 9 2009 10 2009 11 2009 12
;

* CREATE DATASET OF ALL INCLUDED BRANDS;
DATA BRANDS; LENGTH BRAND $200.; INPUT BRAND $ @@;
CARDS;
NUVARING YAZ MIRENA SEASONIQUE LOESTRIN
;

* CREATE CROSS-PRODUCT OF BRAND-YEARMONTHS;
PROC SQL;
    CREATE TABLE BRAND_YEARMONTHS AS
    SELECT BRAND, MONTHLABEL, QTRLABEL, YEAR, MONTH
    FROM BRANDS, YEARMONTHS;
QUIT; RUN;
PROC SORT DATA=BRAND_YEARMONTHS; BY BRAND YEAR MONTH; RUN;

* UPDATE LOCL_TV_MM DATASETS;
DATA WITHDUPS; SET TEMP.LOCL_TV_MM_JAN07_DEC09;
	EQIVWGT=DURATION/60;
	IF BRAND IN ('NUVARING' 'YAZ' 'MIRENA' 'SEASONIQUE' 'LOESTRIN') THEN OUTPUT; RUN;

PROC SORT DATA=WITHDUPS NODUPKEY OUT=LOCL_TV_MM; BY MARKET BRAND REPORT_PERIOD RECTYPE CREATIVE 
										   DAYPART COMMERCIAL_DURATION; RUN;

PROC FREQ DATA=	LOCL_TV_MM; TABLES BRAND*DURATION/LIST MISSING; RUN;

* CREATE DATASET OF ALL INCLUDED DMAS;
DATA TEMP.DMAS; SET TEMP.LOCL_TV_MM_JAN07_DEC09 (KEEP=MARKET); BY MARKET; IF FIRST.MARKET; RUN;

* CREATE CROSS-PRODUCT OF BRAND-YEARMONTHS-DMAS;
PROC SQL;
	CREATE TABLE BRAND_YEARMONTHS_DMAS AS
	SELECT BRAND, MONTHLABEL, YEAR, MONTH, MARKET
	FROM BRAND_YEARMONTHS, TEMP.DMAS;
QUIT;
PROC SORT DATA=BRAND_YEARMONTHS_DMAS; BY BRAND MARKET YEAR MONTH; RUN;

PROC FREQ DATA=LOCL_TV_MM (WHERE=(BRAND='NUVARING')); TABLES BRAND*CREATIVE*YEAR*MONTH/LIST MISSING; RUN;

TITLE2 "LIST CONTENTS OF LOCAL TV AND MULTIMEDIA DATASET"; RUN;
PROC CONTENTS DATA=LOCL_TV_MM VARNUM; RUN; TITLE2 ""; RUN; 

*CREATE INDICATION VARIABLE;
DATA LOCL_TV_MM1;
     SET LOCL_TV_MM;
	 LENGTH FLAG $15.;
	 IF BRAND='YAZ' AND RECTYPE ='TV' AND CREATIVE = 'PMDD/MODERATE ACNE/WOMAN IN CLUB' THEN FLAG= 'CORR'; ELSE
     FLAG='REG';

RUN;

* EXPLORE TV WEIGHTED GRP DATA BY BRAND MARKET YEAR MONTH;
PROC MEANS DATA=LOCL_TV_MM1 (WHERE=(RECTYPE="TV")) SUM NOPRINT NWAY;
CLASS BRAND FLAG MARKET YEAR MONTH;
WEIGHT EQIVWGT;
VAR NETSPOTTV_F1849 SPOTTV_F1849 SYNSPOTTV_F1849;
OUTPUT OUT=LOCL_WSUM SUM(NETSPOTTV_F1849 SPOTTV_F1849 SYNSPOTTV_F1849)=NETSPOTTV_F1849_W SPOTTV_F1849_W SYNSPOTTV_F1849_W; RUN;
PROC SORT DATA=LOCL_WSUM; BY BRAND MARKET YEAR MONTH; RUN;

DATA MLOCL_WSUM (DROP=K); MERGE BRAND_YEARMONTHS_DMAS LOCL_WSUM; BY BRAND MARKET YEAR MONTH;
ARRAY NUMS(*) _NUMERIC_; 
DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; 
END; 
IF FLAG=' ' THEN FLAG='REG';
RUN;
PROC SORT DATA=MLOCL_WSUM; BY MONTHLABEL; RUN;
proc contents data=	LOCL_TV_MM; run;

* EXPLORE TV SPEND DATA BY BRAND MARKET YEAR MONTH;
PROC MEANS DATA=LOCL_TV_MM1 (WHERE=(RECTYPE="TV"))SUM NOPRINT NWAY;
CLASS BRAND FLAG MARKET YEAR MONTH;
VAR  SPOTTV_SPEND ;
OUTPUT OUT=LOCL_SPEND SUM( SPOTTV_SPEND )=  SPOTTV_SPEND ; RUN;
PROC SORT DATA=LOCL_SPEND; BY BRAND MARKET YEAR MONTH; RUN;
DATA MLOCL_SPEND (DROP=K); MERGE BRAND_YEARMONTHS_DMAS LOCL_SPEND; BY BRAND MARKET YEAR MONTH;
ARRAY NUMS(*) _NUMERIC_; DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; END; 
IF FLAG=' ' THEN FLAG='REG'; RUN;

* EXPLORE MULTIMEDIA SPEND DATA BY BRAND MARKET YEAR MONTH;
PROC MEANS DATA=LOCL_TV_MM1 (WHERE=(RECTYPE="MM"))SUM NOPRINT NWAY;
CLASS BRAND MARKET YEAR MONTH;
VAR FSI_SPEND LMAG_SPEND LNEWS_SPEND LSUNSUP_SPEND OUT_SPEND SPOTRADIO_SPEND;
OUTPUT OUT=LOCL_MM SUM(FSI_SPEND LMAG_SPEND LNEWS_SPEND LSUNSUP_SPEND OUT_SPEND SPOTRADIO_SPEND)=
 				       FSI_SPEND LMAG_SPEND LNEWS_SPEND LSUNSUP_SPEND OUT_SPEND SPOTRADIO_SPEND; RUN;
DATA MLOCL_MM (DROP=K); MERGE BRAND_YEARMONTHS_DMAS LOCL_MM; BY BRAND MARKET YEAR MONTH;
ARRAY NUMS(*) _NUMERIC_; DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; END; RUN;

* EXPLORE PRINT GRP BY BRAND MARKET YEAR MONTH;
PROC MEANS DATA=LOCL_TV_MM1 (WHERE=(RECTYPE="PR"))SUM NOPRINT NWAY;
CLASS BRAND MARKET YEAR MONTH;
VAR PRINT_GRP;
OUTPUT OUT=LOCL_PR SUM(PRINT_GRP)=PRINT_GRP; RUN;
DATA MLOCL_PR (DROP=K); MERGE BRAND_YEARMONTHS_DMAS LOCL_PR; BY BRAND MARKET YEAR MONTH;
ARRAY NUMS(*) _NUMERIC_; DO K=1 TO DIM(NUMS); IF NUMS(K)=. THEN NUMS(K)=0; END; RUN;

* CREATE PERMANENT DATASETS FOR MODELING;
DATA NEWOUT.LOCL_TV_WEIGHTED_GRPS (DROP=_TYPE_ _FREQ_); SET MLOCL_WSUM; 
if monthlabel=' ' then monthlabel=PUT((MDY(MONTH,1,YEAR)),MONYY5.);RUN;
DATA NEWOUT.LOCL_TV_SPEND; SET MLOCL_SPEND(DROP=_TYPE_ _FREQ_); RUN;
DATA NEWOUT.LOCL_MM_SPEND; SET MLOCL_MM (DROP=_TYPE_ _FREQ_); RUN;
DATA NEWOUT.LOCL_PRINT; SET MLOCL_PR (DROP=_TYPE_ _FREQ_); RUN;
DATA NEWOUT.LOCL_TV_MM; SET LOCL_TV_MM1; RUN;

* PRINT SAMPLE RECORDS FROM PERMANENT DATASETS;
TITLE2 "LOCAL TV GRPS WEIGHTED BY COMMERCIAL DURATION"; 
PROC PRINT DATA=NEWOUT.LOCL_TV_WEIGHTED_GRPS (OBS=10); RUN;
TITLE2 "LOCAL TV SPEND";
PROC PRINT DATA=NEWOUT.LOCL_TV_MM (OBS=10); RUN;
TITLE2 ""; RUN;
PROC FREQ DATA=NEWOUT.LOCL_TV_MM; TABLES BRAND*DURATION/LIST MISSING; RUN; 
RUN;

PROC FREQ DATA=NEWOUT.LOCL_TV_MM(WHERE=(BRAND='NUVARING')); TABLES rectype*duration*CREATIVE*YEAR*MONTH/LIST MISSING; RUN; 
